


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// НастройкиПрайсаПоставщика.Параметры.УстановитьЗначениеПараметра("Поставщик", Объект.Ссылка);
	ОбновитьСтрокиНастроекЗагрузкиВыгрузки();

	ЛимитыПоставки.Отбор.Элементы.Очистить();
    ЭлементОтбора = ЛимитыПоставки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Поставщик");
    ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
    ЭлементОтбора.Использование = Истина;
    ЭлементОтбора.ПравоеЗначение = Объект.Ссылка;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСтрокиНастроекЗагрузкиВыгрузки()

	Элементы.НадписьНастрокаВыгрузкиПрайсов.Заголовок = "НЕ настроено";
	Элементы.НадписьНастрокаВыгрузкиЗаказов.Заголовок = "НЕ настроено";
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Поставщик", Объект.Ссылка);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	НастройкиПрайсовПоставщиков.Договор.Наименование КАК Договор
			|ИЗ
			|	РегистрСведений.НастройкиПрайсовПоставщиков КАК НастройкиПрайсовПоставщиков
			|ГДЕ
			|	НастройкиПрайсовПоставщиков.Поставщик = &Поставщик";
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Если ВыборкаДетальныеЗаписи.Количество() = 1 Тогда
			ВыборкаДетальныеЗаписи.Следующий();
			Элементы.НадписьНастрокаВыгрузкиПрайсов.Заголовок = "По договору: " + СокрЛП(ВыборкаДетальныеЗаписи.Договор);
		Иначе
			Элементы.НадписьНастрокаВыгрузкиПрайсов.Заголовок = "Изменить";
		КонецЕсли;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	1 КАК Поле
			|ИЗ
			|	РегистрСведений.НастройкиВыгрузкиЗаказовПоставщикам КАК НастройкиВыгрузкиЗаказовПоставщикам
			|ГДЕ
			|	НастройкиВыгрузкиЗаказовПоставщикам.Поставщик = &Поставщик";
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Элементы.НадписьНастрокаВыгрузкиЗаказов.Заголовок = "Изменить";
		КонецЕсли;
	КонецЕсли; 

КонецПроцедуры
 
&НаСервереБезКонтекста
Функция ПолучитьКлючЗаписиНастройкиЗаказов(Ссылка)

	Перем Ключ;
	Выборка = РегистрыСведений.НастройкиВыгрузкиЗаказовПоставщикам.Выбрать(Новый Структура("Поставщик", Ссылка), "Убыв");
	Если Выборка.Следующий() Тогда
		ДанныеКлюча	= Новый Структура("Поставщик", Ссылка);
		Ключ	= РегистрыСведений.НастройкиВыгрузкиЗаказовПоставщикам.СоздатьКлючЗаписи(ДанныеКлюча);
	Иначе
		Ключ	= РегистрыСведений.НастройкиВыгрузкиЗаказовПоставщикам.ПустойКлюч();
	КонецЕсли;
	
	Возврат Ключ;

КонецФункции

&НаКлиенте
Процедура НадписьНастрокаВыгрузкиЗаказовНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, "Сохраните данные контрагента", 20);
		Возврат;
	КонецЕсли; 
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Поставщик", Объект.Ссылка);

	КлючЗаписи = ПолучитьКлючЗаписиНастройкиЗаказов(Объект.Ссылка);
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);

	ОткрытьФорму("РегистрСведений.НастройкиВыгрузкиЗаказовПоставщикам.ФормаЗаписи", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура НадписьНастрокаВыгрузкиПрайсовНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПоказатьПредупреждение(, "Сохраните данные контрагента", 20);
		Возврат;
	КонецЕсли; 

	Отбор = Новый Структура;
	Отбор.Вставить("Поставщик", Объект.Ссылка);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);

	//ОткрытьФорму("РегистрСведений.НастройкиПрайсовПоставщиков.Форма.ФормаНабораЗаписей", ПараметрыФормы);
	ОткрытьФорму("РегистрСведений.НастройкиПрайсовПоставщиков.Форма.ФормаНастройкиПрайсов", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСтрокуНастройкиЗагрузок" Тогда
		ОбновитьСтрокиНастроекЗагрузкиВыгрузки();
	КонецЕсли; 
	
КонецПроцедуры

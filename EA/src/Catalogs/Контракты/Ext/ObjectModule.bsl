
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ДатаОкончания < ДатаНачала Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Дата окончания не должна быть меньше даты начала.'"), Ссылка, "ДатаОкончания", "Объект", Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Родитель) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", Родитель);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Контракты.ДатаНачала,
		|	Контракты.ДатаОкончания
		|ИЗ
		|	Справочник.Контракты КАК Контракты
		|ГДЕ
		|	Контракты.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Если ДатаНачала < Выборка.ДатаНачала ИЛИ ДатаНачала >= Выборка.ДатаОкончания
			ИЛИ ДатаОкончания <= Выборка.ДатаНачала ИЛИ ДатаОкончания > Выборка.ДатаОкончания Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Период плана контракта должен быть в пределах периода контракта.'"), Ссылка, "ДатаНачала", "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Родитель") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения.Родитель);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Контракты.Владелец,
		|	Контракты.ТипКонтракта,
		|	Контракты.ШкалаБюджета,
		|	Контракты.УчитыватьНДС
		|ИЗ
		|	Справочник.Контракты КАК Контракты
		|ГДЕ
		|	Контракты.Ссылка = &Ссылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

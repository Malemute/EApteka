
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("НачальныеДанные") Тогда
		ПодобранныеКонцерны.Загрузить(Параметры.НачальныеДанные.Выгрузить());
	КонецЕсли; 

КонецПроцедуры


&НаСервере
Функция ЕстьГруппа(слкКонцерн)

	Отбор = Новый Структура();
	Отбор.Вставить("Концерн", слкКонцерн);
	Строки = ПодобранныеКонцерны.НайтиСтроки(Отбор);
	Если Строки.Количество() > 0 Тогда
		Возврат Истина;
	КонецЕсли; 
	
	Возврат Ложь;

КонецФункции // ЕстьГруппа()
 

&НаКлиенте
Процедура ПодобранныеКонцерныПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокКонцерныВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЕстьГруппа(ВыбраннаяСтрока) Тогда
		Возврат;
	КонецЕсли; 

	СтрокаТЧ = ПодобранныеКонцерны.Добавить();
	СтрокаТЧ.Концерн = ВыбраннаяСтрока;

КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	
	Перенести = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	СтруктураВозврата = ПриЗакрытииНаСервере();
	
	Если Перенести Тогда
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПриЗакрытииНаСервере()
	
	СтруктураВозврата = Новый Структура();
	Если Перенести Тогда
		АдресПодобранногоВХранилище = ПоместитьПодобранноеВХранилище();
		СтруктураВозврата.Вставить("АдресПодобранногоВХранилище", АдресПодобранногоВХранилище);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервере
Функция ПоместитьПодобранноеВХранилище()

	ТаблицаПодбора = ПодобранныеКонцерны.Выгрузить();
	АдресПодобраннойНоменклатурыВХранилище = ПоместитьВоВременноеХранилище(ТаблицаПодбора, УникальныйИдентификатор);

	Возврат АдресПодобраннойНоменклатурыВХранилище;
	
КонецФункции


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если Не Перенести И ПодобранныеКонцерны.Количество() > 0 Тогда

		Отказ = Истина;

		ТекстВопроса = НСтр("ru = 'Подобранные позиции не перенесены.
			|Перенести?'");
			
		Оповещение = Новый ОписаниеОповещения("ВопросПеренестиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВопросПеренестиЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		Перенести = Истина;
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		Перенести = Истина;
		ПодобранныеКонцерны.Очистить();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры



#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МассивЭлементов = КомпоновкаДанныхКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "Владелец");
	
	Для Каждого ЭлементОтбора Из МассивЭлементов Цикл
		Если ЭлементОтбора.Использование И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			Контрагент = ЭлементОтбора.ПравоеЗначение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НоменклатураКонтрагентов" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		ЗаполнитьНоменклатуруКонтрагента(Контрагент, ВыбранноеЗначение.АдресТоваровВХранилище);
		
		ПоказатьПредупреждение(, НСтр("ru='Заполнение номенклатуры контрагента завершено.'"));
		
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗагрузитьИзТабличногоДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Не выбран контрагент'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Шаг1Подсказка", "Необходимо скопировать колонки в таблицу из внешнего файла (Excel. Word и пр.) через буфер обмена.
	|Обязательно должны быть заполнены колонки: ""Код товара контрагента"" и ""Наименование"".");
	ПараметрыФормы.Вставить("Поставщик", Контрагент);
	ПараметрыФормы.Вставить("ЗагружатьПроизводителя", Истина);
	ПараметрыФормы.Вставить("ВидИмпортируемыхДанных", ПредопределенноеЗначение("Перечисление.ВидыИмпортируемыхДанных.СправочникТоваров"));
	
	ОткрытьФорму("Обработка.ЗагрузкаДанныхИзВнешнихФайлов.Форма.Форма", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзВнешнихФайлов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Контрагент", Контрагент);
	ПараметрыФормы.Вставить("ИмпортСправочникаТоваров", Истина);
	
	ОткрытьФорму("ОбщаяФорма.ИмпортДанныхКонтрагента", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КомпоновкаДанныхКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Владелец",
		Контрагент,
		ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(Контрагент));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНоменклатуруКонтрагента(Контрагент, АдресТоваровВХранилище)
	
	ТаблицаДанных = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Нет данных для заполнения.'");
	КонецЕсли;
	
	ДанныеЗагрузки = Новый Массив;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("КодТовара",                 СтрокаТаблицы.ЗагружаемыйАртикул);
		СтруктураДанных.Вставить("НаименованиеТовара",        СтрокаТаблицы.ЗагружаемаяНоменклатура);
		СтруктураДанных.Вставить("НаименованиеПроизводителя", СтрокаТаблицы.ЗагружаемыйПроизводитель);
		СтруктураДанных.Вставить("Штрихкод",                  СтрокаТаблицы.ЗагружаемыйШтрихкод);
		
		ДанныеЗагрузки.Добавить(СтруктураДанных);
	КонецЦикла;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ДанныеЗагрузки", ДанныеЗагрузки);
	ПараметрыЗагрузки.Вставить("ИдентификаторыПоставщиков", Новый Соответствие);
	
	ОбменДаннымиСКонтрагентами.ЗагрузитьСправочникТоваров(Контрагент, ПараметрыЗагрузки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Запись = РегистрыСведений.СоответствиеНоменклатурыКонтрагентов.СоздатьМенеджерЗаписи();
		Запись.НоменклатураКонтрагента = Объект.Ссылка;
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Номенклатура = Запись.Номенклатура;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Запись = РегистрыСведений.СоответствиеНоменклатурыКонтрагентов.СоздатьМенеджерЗаписи();
	Запись.НоменклатураКонтрагента = ТекущийОбъект.Ссылка;
	Запись.Прочитать();
	
	Если Запись.Выбран() Тогда
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Запись.Удалить();
		ИначеЕсли Запись.Номенклатура <> Номенклатура Тогда
			Запись.Номенклатура = Номенклатура;
			Запись.Записать();
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			Запись.НоменклатураКонтрагента = ТекущийОбъект.Ссылка;
			Запись.Номенклатура = Номенклатура;
			Запись.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Запись = РегистрыСведений.СоответствиеПоставщиковКонтрагентов.СоздатьМенеджерЗаписи();
		Запись.ПоставщикКонтрагента = Объект.Ссылка;
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Контрагент = Запись.Контрагент;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Запись = РегистрыСведений.СоответствиеПоставщиковКонтрагентов.СоздатьМенеджерЗаписи();
	Запись.ПоставщикКонтрагента = ТекущийОбъект.Ссылка;
	Запись.Прочитать();
	
	Если Запись.Выбран() Тогда
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
			Запись.Удалить();
		ИначеЕсли Запись.Контрагент <> Контрагент Тогда
			Запись.Контрагент = Контрагент;
			Запись.Записать();
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Запись.ПоставщикКонтрагента = ТекущийОбъект.Ссылка;
			Запись.Контрагент = Контрагент;
			Запись.Записать();
		КонецЕсли;
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьТаблицуДиапазонов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура СформироватьНаименование(Команда)
	
	Наименование = "";
	
	Для Каждого СтрокаТЧ Из Объект.Диапазоны Цикл
		Если НЕ ПустаяСтрока(Наименование) Тогда
			Наименование = Наименование + "; ";
		КонецЕсли;
		
		Наименование = Наименование + "[" + СтрокаТЧ.Диапазон + "] - " + СтрокаТЧ.Бюджет + "%";
	КонецЦикла;
	
	Объект.Наименование = Наименование;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДиапазоныПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		НастроитьТаблицуДиапазонов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДиапазоныПослеУдаления(Элемент)
	
	НастроитьТаблицуДиапазонов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДиапазоныНижняяГраницаПриИзменении(Элемент)
	
	НастроитьТаблицуДиапазонов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьТаблицуДиапазонов()
	
	Если Объект.Диапазоны.Количество() > 0 Тогда
		Объект.Диапазоны.Сортировать("НижняяГраница");
	КонецЕсли;
	
	Для Индекс = 0 По Объект.Диапазоны.Количество() - 1 Цикл
		
		Если Объект.Диапазоны.Количество() = 1 И Объект.Диапазоны[Индекс].НижняяГраница = 0 Тогда
			Диапазон = "<Весь диапазон>";
		Иначе
			Если Индекс = Объект.Диапазоны.Количество() - 1 Тогда
				Диапазон = "Свыше %НачалоДиапазона%";
				Диапазон = СтрЗаменить(Диапазон, "%НачалоДиапазона%", Объект.Диапазоны[Индекс].НижняяГраница);
			Иначе
				Диапазон = "От %НачалоДиапазона% до %ОкончаниеДиапазона%";
				Диапазон = СтрЗаменить(Диапазон, "%НачалоДиапазона%", Объект.Диапазоны[Индекс].НижняяГраница);
				Диапазон = СтрЗаменить(Диапазон, "%ОкончаниеДиапазона%", Объект.Диапазоны[Индекс + 1].НижняяГраница);
			КонецЕсли;
		КонецЕсли;
		
		Объект.Диапазоны[Индекс].Диапазон = Диапазон;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти



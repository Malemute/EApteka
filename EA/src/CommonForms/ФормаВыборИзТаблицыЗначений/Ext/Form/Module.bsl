//Служебная форма для выбора строки из таблицы значений.
//	Параметры формы:
//	АдресТаблицы 	 - строка, адрес временного хранилища в котором лежит таблица из которой необходимо выбрать строку;
//	ИмяСобытияВыбора - строка, имя события, с которым пройдет оповещение о выборе значения, источник - форма фладелец;
//	СписокКолонок 	 - СписокЗначений, список колонок таблиц,
//					   Значение - имя колонки(должно совпадать с именем ТЗ), иначе колонка не будет создана.
//					   Представление - заголовок колонки;


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаЗначенийПолучаемая = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
	Если ТипЗнч(ТаблицаЗначенийПолучаемая) <> Тип("ТаблицаЗначений") тогда
		Возврат;
	КонецЕсли;
	Если ТаблицаЗначенийПолучаемая.Количество() = 0 тогда
		Возврат
	КонецЕсли;
	МассивРеквизитов = Новый Массив;
	МассивСтроковыхКолонок = Новый Массив;
	Для каждого Колонка из ТаблицаЗначенийПолучаемая.Колонки цикл
		КолонкаТаблицы = Новый РеквизитФормы(Колонка.Имя,Колонка.ТипЗначения,"ТаблицаЗначенийРеквизит",Колонка.Заголовок);
		МассивРеквизитов.Добавить(КолонкаТаблицы);
		МассивТипов = Колонка.ТипЗначения.Типы();
		Для каждого Тип из МассивТипов цикл
			Если Справочники.ТипВсеСсылки().СодержитТип(Тип) или Документы.ТипВсеСсылки().СодержитТип(Тип) тогда
				МассивСтроковыхКолонок.Добавить(КолонкаТаблицы.Имя);
				КолонкаТаблицы = Новый РеквизитФормы(Колонка.Имя+"Строка",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)),"ТаблицаЗначенийРеквизит",Колонка.Заголовок);
				МассивРеквизитов.Добавить(КолонкаТаблицы);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	       		
	ИзменитьРеквизиты(МассивРеквизитов);
	ТаблицаЗначенийРеквизит.Загрузить(ТаблицаЗначенийПолучаемая);
	Для каждого строка из ТаблицаЗначенийРеквизит цикл
		Для каждого Колонка из МассивСтроковыхКолонок цикл
			Строка[Колонка+"Строка"] = Строка[Колонка];
		КонецЦикла;
	КонецЦикла;
	Отказ = Истина;
	Если Параметры.СписокКолонок.Количество() > 0 тогда
		Для каждого Колонка из Параметры.СписокКолонок цикл
			КолонкаВТаблице = ТаблицаЗначенийПолучаемая.Колонки.Найти(Колонка.Значение);
			Если КолонкаВТаблице <> Неопределено тогда
				Дополнение = "";
				Если МассивСтроковыхКолонок.Найти(КолонкаВТаблице.Имя)<> неопределено тогда
					Дополнение = "Строка";
				КонецЕсли;
				НоваяКолонкаЭлемент = Элементы.Добавить("ТЗ"+Колонка.Значение,Тип("ПолеФормы"),Элементы.ТаблицаЗначенийЭлемент);
				Ширина = КолонкаВТаблице.Ширина;
				НоваяКолонкаЭлемент.Ширина = Ширина;
				НоваяКолонкаЭлемент.Заголовок = Колонка.Представление;
				НоваяКолонкаЭлемент.ПутьКДанным = "ТаблицаЗначенийРеквизит."+Колонка.Значение+Дополнение;
				Отказ = Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Отказ = Ложь;
		Для каждого Колонка из ТаблицаЗначенийПолучаемая.Колонки цикл 	
			НоваяКолонкаЭлемент = Элементы.Добавить("ТЗ"+Колонка.Имя,Тип("ПолеФормы"),Элементы.ТаблицаЗначенийЭлемент);
			НоваяКолонкаЭлемент.Ширина = Колонка.Ширина;
			НоваяКолонкаЭлемент.Заголовок = Колонка.Заголовок;
			НоваяКолонкаЭлемент.ПутьКДанным = "ТаблицаЗначенийРеквизит."+Колонка.Имя;
		КонецЦикла;
	КонецЕсли;
	ИмяСобытияВыбора = Параметры.ИмяСобытияВыбора;
	ЭтаФорма.Заголовок = Параметры.Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийЭлементВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураВозврата = ПолучитьСтруктуруВозврата(ВыбраннаяСтрока);
	ЭтаФорма.Закрыть();
	Оповестить(ИмяСобытияВыбора,СтруктураВозврата,ЭтаФорма.ВладелецФормы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруВозврата(ВыбраннаяСтрока)
	
	СтруктураВозврата = Новый Структура;
	Строка = ТаблицаЗначенийРеквизит.Получить(ВыбраннаяСтрока);
	Для каждого Колонка из ТаблицаЗначенийРеквизит.Выгрузить().Колонки цикл
		СтруктураВозврата.Вставить(Колонка.Имя,Строка[Колонка.Имя]);
	КонецЦикла;
	Возврат СтруктураВозврата
	
КонецФункции

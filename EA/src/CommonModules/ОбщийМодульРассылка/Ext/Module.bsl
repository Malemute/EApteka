Функция СформироватьТабличныйДокументПоЗапросу(ТекстЗапроса, СтруктураПараметров = Неопределено, Заголовок = "", ОткрытьГруппировкиСтрок=Истина,ИспользоватьЧередованиеСтрок = Истина, МассивШириныКолонок = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = ТекстЗапроса;
	
	Если СтруктураПараметров <> неопределено тогда
		Для каждого КлючИЗначение из СтруктураПараметров цикл
			Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если СтрНайти(ТекстЗапроса, "ИТОГИ") <> 0 тогда
		Иерархия = Истина;
	Иначе
		Иерархия = Ложь;
	КонецЕсли;
	Попытка
		Результат = Запрос.Выполнить();
	Исключение
		Возврат ОписаниеОшибки();
	КонецПопытки;
	
	ОбработкаКонсольЗапросов = Обработки.КонсольЗапросовДляУправляемогоПриложения.Создать();
	
	Если МассивШириныКолонок = неопределено тогда
		МассивШириныКолонок = Новый Массив;
	КонецЕсли;
	
	Попытка
		РезультатТД = ОбработкаКонсольЗапросов.ВывестиРезультатОдногоЗапроса(Заголовок,Результат,ОткрытьГруппировкиСтрок,Ложь,"Авто",Иерархия,0,МассивШириныКолонок,ИспользоватьЧередованиеСтрок,Истина);
	Исключение
		Возврат ОписаниеОшибки();
	КонецПопытки;

	Если ОткрытьГруппировкиСтрок тогда
		РезультатТД.ПоказатьУровеньГруппировокСтрок(РезультатТД.КоличествоУровнейГруппировокСтрок());	
	КонецЕсли;
	
	Возврат РезультатТД;	
	
КонецФункции

Функция СохранитьТабличныйДокументВФайл(ТабличныйДокумент,Заголовок) Экспорт
	ИмяФайла = КаталогВременныхФайлов()+Заголовок+".xls";            
	ТабличныйДокумент.Записать(ИмяФайла,ТипФайлаТабличногоДокумента.XLS);
	Возврат ИмяФайла	
КонецФункции

Процедура ОтправитьПисьмоПоШаблону(Шаблон) Экспорт
	ТекущаяДата = ТекущаяДата();
	ВложенияПисьмаОписание = Шаблон.ВложенияТабличныеДокументы.Выгрузить();
	МассивВложений = Новый Массив;
	МассивОшибок = Новый Массив;
	Для каждого ОписаниеВложения из ВложенияПисьмаОписание цикл
		СтруктураПараметров = Неопределено;
		Выполнить(ОписаниеВложения.КодФормированияПараметраЗапросов);
		Заголовок = ОписаниеВложения.ШаблонИмениВложения;
		Заголовок = СтрЗаменить(Заголовок,"[ТекущаяДата]",Формат(ТекущаяДата,ОписаниеВложения.ФорматнаяСтрокаДаты));
		ТабличныйДокумент = СформироватьТабличныйДокументПоЗапросу(ОписаниеВложения.ТекстЗапроса,СтруктураПараметров,Заголовок);	
		Если ТипЗнч(ТабличныйДокумент) = Тип("Строка") тогда
			МассивОшибок.Добавить(ТабличныйДокумент);
			Продолжить;
		КонецЕсли;
		Попытка
			МассивВложений.Добавить(СохранитьТабличныйДокументВФайл(ТабличныйДокумент,Заголовок));
		Исключение
			МассивОшибок.Добавить(ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
	Если МассивОшибок.Количество() > 0 тогда
		ЗафиксироватьОтправкуПисьмаВЛоге(Шаблон,ТекущаяДата,МассивОшибок);
		Возврат;
	КонецЕсли;
	ТелоПисьма = Шаблон.ТелоПисьма;
	ТелоПисьма = СтрЗаменить(ТелоПисьма,"[ТекущаяДата]",Формат(ТекущаяДата,Шаблон.ФорматнаяСтрокаДаты));
	
	ТемаПисьма = Шаблон.ТемаПисьма;
	ТемаПисьма = СтрЗаменить(ТемаПисьма,"[ТекущаяДата]",Формат(ТекущаяДата,Шаблон.ФорматнаяСтрокаДаты));
	
	ТипТекстаСообщения = ТипТекстаПочтовогоСообщения.ПростойТекст;
	
	ТипТекста = Шаблон.ТипТекстаПочтовогоСообщения;
	Если ТипТекста = Перечисления.ТипТекстаПочтовогоСообщения.HTML тогда 
		ТипТекстаСообщения = ТипТекстаПочтовогоСообщения.HTML;
	ИначеЕсли ТипТекста = Перечисления.ТипТекстаПочтовогоСообщения.РазмеченныйТекст тогда
		ТипТекстаСообщения = ТипТекстаПочтовогоСообщения.РазмеченныйТекст;
	КонецЕсли;
	
	Попытка
		Справочники.УчетныеЗаписиЭлектроннойПочты.ОтправитьЭлектронноеПисьмо(Шаблон.УчетнаяЗаписьЭлектроннойПочты,ТипТекстаСообщения, ТемаПисьма,Шаблон.ПолучателиРассылки,МассивВложений,ТелоПисьма);
	Исключение
		МассивОшибок.Добавить(ОписаниеОшибки());		
	КонецПопытки;
	
	ЗафиксироватьОтправкуПисьмаВЛоге(Шаблон,ТекущаяДата,МассивОшибок);	
	
КонецПроцедуры

Процедура ЗафиксироватьОтправкуПисьмаВЛоге(Шаблон,Период,МассивОшибок)
	
	Ошибка = "";
	Для каждого ОписаниеОшибки из МассивОшибок цикл
		Ошибка = Ошибка + ОписаниеОшибки + Символы.ПС;
	КонецЦикла;
	ПисьмоОтправлено = СтрДлина(Ошибка) = 0;
	
	МенеджерЛога = РегистрыСведений.ЛогРассылкиПоЭлектроннойПочте.СоздатьМенеджерЗаписи();
	
	МенеджерЛога.Период = Период;
	МенеджерЛога.ШаблонРассылки = Шаблон;
	МенеджерЛога.ПисьмоОтправлено = ПисьмоОтправлено;
	МенеджерЛога.ОписаниеОшибки = Ошибка;
	
	МенеджерЛога.Записать();
	
КонецПроцедуры




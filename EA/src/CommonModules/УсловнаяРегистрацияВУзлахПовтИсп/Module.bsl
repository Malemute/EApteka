
// Функция - Получить структуру активных настроек условной регистрации для вида объекта
//    Определяет наличие в системе настроек условной регистрации для вида объекта
//
// Параметры:
//  ВидОбъекта	 - СправочникСсылка.ВидыОбъектовМ - вид объекта, для которого ищутся настройки условной регисрации
// 
// Возвращаемое значение:
//   - Строка - структура, закодированная командой ЗначениеВСтрокуВнутр, 
//              структура содержит ключи "МассивНастроекБезУсловий" и "МассивНастроекПоУсловиям", а также ключ "ТаблицаУзловНастроек"
//              ключ МассивНастроекБезУсловий содержит массив настроек с УСТАНОВЛЕННОЙ опцией "ВсеБезУсловий"
//              ключ МассивНастроекПоУсловиям содержит массив настроек со СНЯТОЙ опцией "ВсеБезУсловий"
//              ключ ТаблицаУзловНастроек содержит таблицу значений с узлами для каждой найденной настроеки (колонки "Настройка" и "Узел")
//
Функция ПолучитьСтруктуруАктивныхНастроекУсловнойРегистрацииДляВидаОбъекта( ВидОбъекта ) Экспорт
	
	УстановитьПривилегированныйРежим( Истина );
	
	// запросом выбираются настройки условной регистриции "без условий" и "по условиям"
	// причем для настроек "по условиям" исключаются те настройки, в которых Узлы повторяют те, что есть в настройках "без условий"
	// это нужно чтобы избежать ненужных проверок
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр( "ВидОбъекта", ВидОбъекта );
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиУсловнойРегистрацииВУзлахПлановОбменаВидыОбъектов.Ссылка КАК Настройка,
	|	НастройкиУсловнойРегистрацииВУзлахПлановОбменаВидыОбъектов.ВсеБезУсловий КАК БезУсловий
	|ПОМЕСТИТЬ табАктивныхНастроек
	|ИЗ
	|	Справочник.НастройкиУсловнойРегистрацииВУзлахПлановОбмена.ВидыОбъектов КАК НастройкиУсловнойРегистрацииВУзлахПлановОбменаВидыОбъектов
	|ГДЕ
	|	НастройкиУсловнойРегистрацииВУзлахПлановОбменаВидыОбъектов.Регистрировать = ИСТИНА
	|	И НастройкиУсловнойРегистрацииВУзлахПлановОбменаВидыОбъектов.ВидОбъекта = &ВидОбъекта
	|	И НастройкиУсловнойРегистрацииВУзлахПлановОбменаВидыОбъектов.Ссылка.НастройкаАктивна = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	табАктивныхНастроек.Настройка КАК Настройка,
	|	табАктивныхНастроек.БезУсловий КАК БезУсловий,
	|	НастройкиУсловнойРегистрацииВУзлахПлановОбменаУзлыПлановОбмена.Узел КАК Узел
	|ПОМЕСТИТЬ табУзловАктивныхНастроек
	|ИЗ
	|	табАктивныхНастроек КАК табАктивныхНастроек,
	|	Справочник.НастройкиУсловнойРегистрацииВУзлахПлановОбмена.УзлыПлановОбмена КАК НастройкиУсловнойРегистрацииВУзлахПлановОбменаУзлыПлановОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	табУзловАктивныхНастроек.Узел
	|ПОМЕСТИТЬ табУзловРегистрацииБезУсловий
	|ИЗ
	|	табУзловАктивныхНастроек КАК табУзловАктивныхНастроек
	|ГДЕ
	|	табУзловАктивныхНастроек.БезУсловий = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	табАктивныхНастроек.Настройка КАК Настройка
	|ИЗ
	|	табАктивныхНастроек КАК табАктивныхНастроек
	|ГДЕ
	|	табАктивныхНастроек.БезУсловий = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	табУзловАктивныхНастроек.Настройка КАК Настройка
	|ИЗ
	|	табУзловАктивныхНастроек КАК табУзловАктивныхНастроек
	|ГДЕ
	|	табУзловАктивныхНастроек.БезУсловий = ЛОЖЬ
	|	И НЕ табУзловАктивныхНастроек.Узел В
	|				(ВЫБРАТЬ
	|					табУзловРегистрацииБезУсловий.Узел
	|				ИЗ
	|					табУзловРегистрацииБезУсловий КАК табУзловРегистрацииБезУсловий)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	табУзловАктивныхНастроек.Настройка КАК Настройка,
	|	табУзловАктивныхНастроек.Узел КАК Узел
	|ИЗ
	|	табУзловАктивныхНастроек КАК табУзловАктивныхНастроек
	|ГДЕ
	|	табУзловАктивныхНастроек.БезУсловий = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	табУзловАктивныхНастроек.Настройка,
	|	табУзловАктивныхНастроек.Узел
	|ИЗ
	|	табУзловАктивныхНастроек КАК табУзловАктивныхНастроек
	|ГДЕ
	|	табУзловАктивныхНастроек.БезУсловий = ЛОЖЬ
	|	И НЕ табУзловАктивныхНастроек.Узел В
	|				(ВЫБРАТЬ
	|					табУзловРегистрацииБезУсловий.Узел
	|				ИЗ
	|					табУзловРегистрацииБезУсловий КАК табУзловРегистрацииБезУсловий)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураАктивныхНастроек = Новый Структура();
	СтруктураАктивныхНастроек.Вставить( "МассивНастроекБезУсловий", МассивРезультатов[3].Выгрузить().ВыгрузитьКолонку( "Настройка" ) );
	СтруктураАктивныхНастроек.Вставить( "МассивНастроекПоУсловиям", МассивРезультатов[4].Выгрузить().ВыгрузитьКолонку( "Настройка" ) );
	СтруктураАктивныхНастроек.Вставить( "ТаблицаУзловНастроек", МассивРезультатов[5].Выгрузить() );
	
	Возврат ЗначениеВСтрокуВнутр( СтруктураАктивныхНастроек );
	
КонецФункции
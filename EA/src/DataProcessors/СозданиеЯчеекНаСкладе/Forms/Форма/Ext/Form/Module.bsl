
&НаСервере
Процедура СоздатьЯчейкиНаСервере()
	Склад = Зона.Склад;
	
	МассивСообщенийОбОшибках = Новый Массив;
	
	Если СтелажМин = 0 тогда
		МассивСообщенийОбОшибках.Добавить("Стелаж ""с"" не может быть равен нулю!!!");	
	КонецЕсли;
	Если ЯчейкаМин = 0 тогда
		МассивСообщенийОбОшибках.Добавить("Ячейка ""с"" не может быть равна нулю!!!");	
	КонецЕсли;
	Если  СтелажМакс < СтелажМин ТОГДА
		МассивСообщенийОбОшибках.Добавить("Стелаж ""с"" меньше стелажа ""по""!!!");
	КонецЕсли;
	Если  ЯчейкаМакс < ЯчейкаМин ТОГДА
		МассивСообщенийОбОшибках.Добавить("Ячейка ""с"" меньше ячейки ""по""!!!");
	КонецЕсли;
	Если  Объем = 0 ТОГДА
		МассивСообщенийОбОшибках.Добавить("Объем не может быть равен 0");
	КонецЕсли;
	
	Если ВиМестаЗСЯ = Перечисления.ВидыМестЗСЯ.ПустаяСсылка() ТОГДА
		МассивСообщенийОбОшибках.Добавить("Необходимо заполнить вид места зся!!!");
	КонецЕсли;

	
	Если МассивСообщенийОбОшибках.Количество() > 0 тогда
		Для каждого строка из МассивСообщенийОбОшибках цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = строка;
			Сообщение.Сообщить();
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	МассивНаименованийЯчеек = Новый Массив;
	
	Для Стел = СтелажМин по СтелажМакс цикл
		Для ЯчейкаНомер = ЯчейкаМин по ЯчейкаМакс цикл
			Ячейка = Справочники.МестоЗСЯ.СоздатьЭлемент();
			Ячейка.Зона = Зона.Наименование;
			Ячейка.ЗонаСсылка = Зона;
			Ячейка.ВидМестаЗСЯ = ВиМестаЗСЯ;
			Ячейка.Отдел = Склад;
			Ячейка.Объем = Объем;
			Ячейка.Стеллаж = стел;
			Ячейка.Ячейка = Формат(ЯчейкаНомер,"ЧДЦ=; ЧН=0; ЧГ=");
			Номер =  СокрЛП(Ячейка.Зона) + "_"+СокрЛП(Ячейка.Стеллаж) + "_" + Ячейка.Ячейка;
			Ячейка.Наименование = Номер;
			МассивНаименованийЯчеек.Добавить(Номер);
			Если ЭтоНоваяЯчейка(Склад, Номер) тогда
				Ячейка.Записать();			
			Иначе
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Ячейка с номером " + Номер + " на складе " + Строка(Склад) + " уже существует!!!";
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МестоЗСЯ.Ссылка как Ячейка,
	|	ЛОЖЬ как Печатать 
	|ИЗ
	|	Справочник.МестоЗСЯ КАК МестоЗСЯ
	|ГДЕ
	|	МестоЗСЯ.ЗонаСсылка = &Зона
	|	И МестоЗСЯ.Наименование в (&МассивНаименованийЯчеек)";
	Запрос.УстановитьПараметр("Зона",Зона);
	Запрос.УстановитьПараметр("МассивНаименованийЯчеек",МассивНаименованийЯчеек);
	ТаблицаЯчейкиЗСЯ.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЯчейки(Команда)
	
	СоздатьЯчейкиНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ЭтоНоваяЯчейка(Склад,Номер)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МестоЗСЯ.Ссылка
	|ИЗ
	|	Справочник.МестоЗСЯ КАК МестоЗСЯ
	|ГДЕ
	|	МестоЗСЯ.Наименование = &Наименование
	|	И МестоЗСЯ.Отдел = &Отдел";
	Запрос.УстановитьПараметр("Отдел",Склад);
	Запрос.УстановитьПараметр("Наименование",Номер);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервере
Функция НапечататьШКНаСервере()
	
	МассивСтрокЗСЯДляПечати = ТаблицаЯчейкиЗСЯ.НайтиСтроки(Новый Структура("Печатать",Истина));
	МассивЗСЯ = ТаблицаЯчейкиЗСЯ.Выгрузить(МассивСтрокЗСЯДляПечати).ВыгрузитьКолонку("Ячейка");
	
	Возврат Обработки.ПечатьШКМестХраненияЗСЯ.СформироватьТабличныйДокументС_ШК_ЗСЯ(МассивЗСЯ);
	
КонецФункции

&НаКлиенте
Процедура НапечататьШК(Команда)
	
	ТД = НапечататьШКНаСервере();
	ТД.Показать();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметки(Команда)
	Отметка = Команда.Имя = "УстановитьОтметки";
	Для каждого строка из ТаблицаЯчейкиЗСЯ цикл
		Строка.Печатать = Отметка;
	КонецЦикла;
КонецПроцедуры


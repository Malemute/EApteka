
&НаСервере
Процедура ЗаполнитьНаСервере()

	Запрос = Новый Запрос;
	ГраницаКонтроля = Новый Граница(Объект.Дата, ВидГраницы.Включая);
	Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДефектураОстатки.Поставщик КАК Поставщик,
		|	ДефектураОстатки.Товар КАК Товар,
		|	ДефектураОстатки.Заказ КАК Заказ,
		|	ДефектураОстатки.КлючСвязи КАК КлючСвязи,
		|	ДефектураОстатки.ДатаДоставки КАК ДатаДоставки,
		|	ДефектураОстатки.ЦенаЗакупки КАК ЦенаЗакупки,
		|	ДефектураОстатки.СрокГодности КАК СрокГодности,
		|	СУММА(ДефектураОстатки.КоличествоОстаток) КАК Количество
		|ИЗ
		|	РегистрНакопления.Дефектура.Остатки(
		|			&МоментВремени,
		|			Склад = &Склад
		|				И Поставщик <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				И ДатаДоставки < &Дата) КАК ДефектураОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ДефектураОстатки.Товар,
		|	ДефектураОстатки.Заказ,
		|	ДефектураОстатки.Поставщик,
		|	ДефектураОстатки.ДатаДоставки,
		|	ДефектураОстатки.СрокГодности,
		|	ДефектураОстатки.КлючСвязи,
		|	ДефектураОстатки.ЦенаЗакупки";

	Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Склад) Тогда

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен склад";
		Сообщение.Поле = "Склад";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		Возврат;

	КонецЕсли; 

	ЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда

		Если НЕ ЗначениеЗаполнено(Объект.Склад) Тогда

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнен склад";
			Сообщение.Поле = "Склад";
			Сообщение.ПутьКДанным = "Объект";
			Сообщение.Сообщить();
			Отказ = Истина;

		КонецЕсли; 

		Если Объект.Товары.Количество() = 0 Тогда

			ПоказатьПредупреждение(, "Пустой документ не может быть проведен", 15);
			Отказ = Истина;

		КонецЕсли; 
	
	КонецЕсли; 
КонецПроцедуры

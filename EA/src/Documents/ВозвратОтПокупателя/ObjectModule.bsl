

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯ.Очистить();
	Движения.КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯ.Записывать = Истина;
	
	Движения.ХранениеЗСЯ.Очистить();
	Движения.ХранениеЗСЯ.Записывать = Истина;
	
	Движения.ХранениеЗСЯПоТоварам.Очистить();
	Движения.ХранениеЗСЯПоТоварам.Записывать = Истина;
	
	Движения.ТоварыКСборке.Очистить();
	Движения.ТоварыКСборке.Записывать = Истина;
	
	Движения.ОстаткиТовара.Записывать = Истина;	
	Движения.ОстаткиТовара.Очистить();
	
	Если не СписаниеНеНайдено тогда
		
		ТаблицаЗаказов = Возвраты.Выгрузить(,"Документ,ТипКомплектацииЗаказа,ЗСЯ"); 
		ТаблицаЗаказов.Свернуть("Документ,ТипКомплектацииЗаказа,ЗСЯ");
		
		ТаблицаЗаказовСборкаВАптеке = ТаблицаЗаказов.Скопировать(Новый Структура("ТипКомплектацииЗаказа",Перечисления.ТипКомплектацииЗаказа.СборкаИКомплектацияВАптеке),"Документ,ЗСЯ");
		МассивЗаказовСборкаВАптеке = ТаблицаЗаказовСборкаВАптеке.ВыгрузитьКолонку("Документ");
		
		ТаблицаЗаказовКомплектацияИзКоробки = ТаблицаЗаказов.Скопировать(Новый Структура("ТипКомплектацииЗаказа",Перечисления.ТипКомплектацииЗаказа.СборкаНаСкладеВКоробку_КомплектацияВАптеке),"Документ,ЗСЯ");
		МассивЗаказовКомплектацияИзКоробки = ТаблицаЗаказовКомплектацияИзКоробки.ВыгрузитьКолонку("Документ");
		
		
		Для каждого Строка из Возвраты цикл
			Если Строка.Товар.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга тогда
				Продолжить;
			КонецЕсли;
			
			ДокументЗаполнен = ЗначениеЗаполнено(Строка.Документ);
			РеализацияПоЗаказу = ТипЗнч(Строка.Документ) = Тип("ДокументСсылка.Заказ") и ДокументЗаполнен;		
			СборкаЗаказаВАптеке = МассивЗаказовСборкаВАптеке.Найти(строка.Документ) <> неопределено и РеализацияПоЗаказу;
			СборкаЗаказаНаСкладеВКоробку = МассивЗаказовКомплектацияИзКоробки.Найти(строка.Документ) <> неопределено и РеализацияПоЗаказу;
			
			Если СборкаЗаказаВАптеке или СборкаЗаказаНаСкладеВКоробку  тогда				
				Движение = Движения.ХранениеЗСЯПоТоварам.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Склад = Отдел;
				Движение.Документ = Строка.Документ;
				Движение.Товар = Строка.Товар;
				Движение.Партия = Строка.Партия;
				Движение.ЗСЯ = Строка.ЗСЯ;
				Движение.Количество = Строка.Количество;
			КонецЕсли;
			
			Движение = Движения.ОстаткиТовара.ДобавитьПриход();
			ДВижение.Период = Дата;
			ДВижение.Товар = Строка.Товар;
			Движение.Партия = Строка.Партия;
			Движение.Отдел = Отдел;
			Движение.Остаток = Строка.Количество;
			Движение.ТипОперации = Перечисления.ТипыОпераций.ВозвратБезККМ;			
		КонецЦикла;	
		
		ОбщиеФункцииСервер.СоздатьЦеныПартии(Отдел,Дата,Возвраты.ВыгрузитьКолонку("Партия"));
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаЗаказов.ТипКомплектацииЗаказа,
		|	ТаблицаЗаказов.Документ,
		|	ТаблицаЗаказов.ЗСЯ
		|ПОМЕСТИТЬ втТаблицаЗаказов
		|ИЗ
		|	&ТаблицаЗаказов КАК ТаблицаЗаказов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТаблицаЗаказов.Документ,
		|	втТаблицаЗаказов.ЗСЯ
		|ПОМЕСТИТЬ втТаблицаЗаказовДляСписания
		|ИЗ
		|	втТаблицаЗаказов КАК втТаблицаЗаказов
		|ГДЕ
		|	втТаблицаЗаказов.ТипКомплектацииЗаказа <> ЗНАЧЕНИЕ(Перечисление.ТипКомплектацииЗаказа.СборкаНаСкладеВПакеты_РаскладкаПакетовВАптеке)
		|	И втТаблицаЗаказов.ЗСЯ <> ЗНАЧЕНИЕ(Справочник.МестоЗСЯ.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ,
		|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ
		|ИЗ
		|	РегистрСведений.КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯ.СрезПоследних(
		|			&Граница,
		|			ЗСЯ В
		|				(ВЫБРАТЬ
		|					втТаблицаЗаказовДляСписания.ЗСЯ
		|				ИЗ
		|					втТаблицаЗаказовДляСписания КАК втТаблицаЗаказовДляСписания)) КАК КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаЗаказовДляСписания КАК втТаблицаЗаказовДляСписания
		|		ПО КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ = втТаблицаЗаказовДляСписания.ЗСЯ
		|			И КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ = втТаблицаЗаказовДляСписания.Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ,
		|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ";
		Запрос.УстановитьПараметр("Граница",Новый Граница(МоментВремени(),ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("ТаблицаЗаказов",ТаблицаЗаказов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() цикл
			ДвижениеЗСЯ = Движения.КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯ.Добавить();	
			ДвижениеЗСЯ.Период = Дата;
			ДвижениеЗСЯ.Регистратор = Ссылка;
			ДвижениеЗСЯ.ЗСЯ = Выборка.ЗСЯ;
			ДвижениеЗСЯ.Заказ = Документы.Заказ.ПустаяСсылка();		
		КонецЦикла;

		
		Если не ЗначениеЗаполнено(ДокументОснование) тогда
			Для каждого строка из СписаниеПакетов цикл
				ДвижениеЗСЯ = Движения.ХранениеЗСЯ.ДобавитьРасход();	
				ДвижениеЗСЯ.Период = Дата;
				ДвижениеЗСЯ.Регистратор = Ссылка;
				ДвижениеЗСЯ.Склад = Отдел;
				ДвижениеЗСЯ.МестоЗаказа = строка.МестоЗаказа;
				ДвижениеЗСЯ.ЗСЯ = строка.ЗСЯ;
				ДвижениеЗСЯ.Документ = строка.Заказ;
				ДвижениеЗСЯ.Количество = строка.Количество;
			КонецЦикла;		
						
		
			ТаблицаЗаказов.Свернуть("Документ");
			Для каждого строка из ТаблицаЗаказов цикл
				СтатусыДокументов.УстановитьСтатусСборкиЗаказа(Строка.Документ,Ссылка,Справочники.СтатусыОбработкиЗаказа.ПолныйВозврат,ПараметрыСеанса.ТекущийПользователь,Дата);
			КонецЦикла;
		КонецЕсли;	
	Иначе
		Для каждого строка из НеХватило цикл
			Движение = Движения.ТоварыКСборке.ДобавитьРасход();		
			Движение.Документ = Строка.Документ;
			Движение.Регистратор = Ссылка;
			Движение.Период = Дата;
			Движение.КоличествоНеХватило = строка.КоличествоНеХватило;
			Движение.Партия = Строка.Партия;
			Движение.Склад = Отдел;
			Движение.Товар = Строка.Товар;
			Движение.ТипСборки = Строка.ТипСборки;
		КонецЦикла;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Чеки") и ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		
		// Заполнение шапки
		Дата = ДанныеЗаполнения.Дата;
		Отдел = ДанныеЗаполнения.Отдел;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		КодОперации = Перечисления.ТипыОпераций.ВозвратБезККМ;
		СписаниеНеНайдено = Ложь;
		
		//Заполним Табличные части
		Возвраты.Очистить();
		СписаниеПакетов.Очистить();
		НеХватило.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЧекиРеализация.Документ КАК Документ,
		|	ЧекиРеализация.Товар КАК Товар,
		|	ЧекиРеализация.Партия КАК Партия,
		|	СУММА(ЧекиРеализация.Количество) КАК Количество,
		|	ЧекиРеализация.ЗСЯ КАК ЗСЯ
		|ПОМЕСТИТЬ СоставЧека
		|ИЗ
		|	Документ.Чеки.Реализация КАК ЧекиРеализация
		|ГДЕ
		|	ЧекиРеализация.Ссылка = &Ссылка
		|	И ЧекиРеализация.Количество > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ЧекиРеализация.Документ,
		|	ЧекиРеализация.Товар,
		|	ЧекиРеализация.Партия,
		|	ЧекиРеализация.ЗСЯ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХранениеЗСЯПоТоварамОстатки.Склад,
		|	ХранениеЗСЯПоТоварамОстатки.Документ,
		|	ХранениеЗСЯПоТоварамОстатки.Товар,
		|	ХранениеЗСЯПоТоварамОстатки.Партия,
		|	ХранениеЗСЯПоТоварамОстатки.ЗСЯ,
		|	ХранениеЗСЯПоТоварамОстатки.КоличествоОстаток - ЕСТЬNULL(СоставЧека.Количество, 0) КАК Количество,
		|	ВЫРАЗИТЬ(ХранениеЗСЯПоТоварамОстатки.Документ КАК Документ.Заказ).ТипКомплектацииЗаказа КАК ТипКомплектацииЗаказа
		|ИЗ
		|	РегистрНакопления.ХранениеЗСЯПоТоварам.Остатки(
		|			&Граница,
		|			Документ В
		|					(ВЫБРАТЬ
		|						СоставЧека.Документ
		|					ИЗ
		|						СоставЧека КАК СоставЧека)
		|				И Склад = &Склад) КАК ХранениеЗСЯПоТоварамОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ СоставЧека КАК СоставЧека
		|		ПО ХранениеЗСЯПоТоварамОстатки.Документ = СоставЧека.Документ
		|			И ХранениеЗСЯПоТоварамОстатки.Товар = СоставЧека.Товар
		|			И ХранениеЗСЯПоТоварамОстатки.Партия = СоставЧека.Партия
		|			И ХранениеЗСЯПоТоварамОстатки.ЗСЯ = СоставЧека.ЗСЯ
		|ГДЕ
		|	ХранениеЗСЯПоТоварамОстатки.КоличествоОстаток - ЕСТЬNULL(СоставЧека.Количество, 0) > 0
		|
		|ДЛЯ ИЗМЕНЕНИЯ
		|	РегистрНакопления.ХранениеЗСЯПоТоварам.Остатки";
		Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
		Запрос.УстановитьПараметр("Склад",Отдел);
		Запрос.УстановитьПараметр("Граница",Новый Граница(ДанныеЗаполнения.МоментВремени(),ВидГраницы.Исключая));
		Результат = Запрос.Выполнить();
		Если не Результат.Пустой() тогда
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() цикл
				ЗаполнитьЗначенияСвойств(Возвраты.Добавить(),Выборка);		
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") и ДанныеЗаполнения.Свойство("Расформирование") Тогда
		
		// Заполнение шапки
		Дата = ДанныеЗаполнения.Дата;
		Отдел = ДанныеЗаполнения.Отдел;
		ДокументОснование = Документы.Чеки.ПустаяСсылка();
		КодОперации = Перечисления.ТипыОпераций.ВозвратБезККМ;
		СписаниеНеНайдено = Ложь;
		
		Возвраты.Очистить();
		СписаниеПакетов.Очистить();
		НеХватило.Очистить();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказТовар.Ссылка КАК Документ,
		|	ЗаказТовар.Ссылка.ТипКомплектацииЗаказа,
		|	ЗаказТовар.Товар,
		|	ЗаказТовар.Партия,
		|	ЗаказТовар.Количество,
		|	ЗНАЧЕНИЕ(Справочник.МестоЗСЯ.ПустаяСсылка) КАК ЗСЯ
		|ПОМЕСТИТЬ втСоставЗаказовПакеты
		|ИЗ
		|	Документ.Заказ.Товар КАК ЗаказТовар
		|ГДЕ
		|	ЗаказТовар.Ссылка В (&МассивЗаказов)
		|	И ЗаказТовар.Ссылка.ТипКомплектацииЗаказа = ЗНАЧЕНИЕ(Перечисление.ТипКомплектацииЗаказа.СборкаНаСкладеВПакеты_РаскладкаПакетовВАптеке)
		|	И ЗаказТовар.Количество > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХранениеЗСЯПоТоварамОстатки.Документ,
		|	ХранениеЗСЯПоТоварамОстатки.Документ.ТипКомплектацииЗаказа КАК ТипКомплектацииЗаказа,
		|	ХранениеЗСЯПоТоварамОстатки.Товар,
		|	ХранениеЗСЯПоТоварамОстатки.Партия,
		|	ХранениеЗСЯПоТоварамОстатки.КоличествоОстаток КАК Количество,
		|	ХранениеЗСЯПоТоварамОстатки.ЗСЯ
		|ПОМЕСТИТЬ втСоставКомплектуемыхЗаказов
		|ИЗ
		|	РегистрНакопления.ХранениеЗСЯПоТоварам.Остатки(
		|			&Граница,
		|			Документ В (&МассивЗаказов)
		|				И (Документ.ТипКомплектацииЗаказа = ЗНАЧЕНИЕ(Перечисление.ТипКомплектацииЗаказа.СборкаНаСкладеВКоробку_КомплектацияВАптеке)
		|					ИЛИ Документ.ТипКомплектацииЗаказа = ЗНАЧЕНИЕ(Перечисление.ТипКомплектацииЗаказа.СборкаИКомплектацияВАптеке))
		|				И Склад = &Склад) КАК ХранениеЗСЯПоТоварамОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Документ КАК Документ,
		|	ВложенныйЗапрос.ТипКомплектацииЗаказа,
		|	ВложенныйЗапрос.Товар КАК Товар,
		|	ВложенныйЗапрос.Партия,
		|	ВложенныйЗапрос.Количество КАК Количество,
		|	ВложенныйЗапрос.ЗСЯ КАК ЗСЯ
		|ИЗ
		|	(ВЫБРАТЬ
		|		втСоставЗаказовПакеты.Документ КАК Документ,
		|		втСоставЗаказовПакеты.Товар КАК Товар,
		|		втСоставЗаказовПакеты.Партия КАК Партия,
		|		втСоставЗаказовПакеты.Количество КАК Количество,
		|		втСоставЗаказовПакеты.ТипКомплектацииЗаказа КАК ТипКомплектацииЗаказа,
		|		втСоставЗаказовПакеты.ЗСЯ КАК ЗСЯ
		|	ИЗ
		|		втСоставЗаказовПакеты КАК втСоставЗаказовПакеты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		втСоставКомплектуемыхЗаказов.Документ,
		|		втСоставКомплектуемыхЗаказов.Товар,
		|		втСоставКомплектуемыхЗаказов.Партия,
		|		втСоставКомплектуемыхЗаказов.Количество,
		|		втСоставКомплектуемыхЗаказов.ТипКомплектацииЗаказа,
		|		втСоставКомплектуемыхЗаказов.ЗСЯ
		|	ИЗ
		|		втСоставКомплектуемыхЗаказов КАК втСоставКомплектуемыхЗаказов) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	Документ,
		|	ЗСЯ,
		|	Товар,
		|	Количество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХранениеЗСЯОстатки.Документ КАК Заказ,
		|	ХранениеЗСЯОстатки.МестоЗаказа,
		|	ХранениеЗСЯОстатки.ЗСЯ,
		|	ХранениеЗСЯОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ХранениеЗСЯ.Остатки(
		|			&Граница,
		|			Документ В (&МассивЗаказов)
		|				И Склад = &Склад) КАК ХранениеЗСЯОстатки";
		Запрос.УстановитьПараметр("МассивЗаказов",ДанныеЗаполнения.МассивЗаказов);
		Запрос.УстановитьПараметр("Склад",Отдел);
		Запрос.УстановитьПараметр("Граница",Новый Граница(МоментВремени(),ВидГраницы.Исключая));
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		РезультатВозвраты = МассивРезультатов[2];
		РезультатРасформированиеПакеты = МассивРезультатов[3];
		
		ВыборкаВозвраты = РезультатВозвраты.Выбрать();
		Пока ВыборкаВозвраты.Следующий() цикл
			ЗаполнитьЗначенияСвойств(Возвраты.Добавить(),ВыборкаВозвраты);
		КонецЦикла;
		
		ВыборкаПакеты = РезультатРасформированиеПакеты.Выбрать();
		Пока ВыборкаПакеты.Следующий() цикл
			ЗаполнитьЗначенияСвойств(СписаниеПакетов.Добавить(),ВыборкаПакеты);	
		КонецЦикла;		
		
	КонецЕсли;
	
КонецПроцедуры



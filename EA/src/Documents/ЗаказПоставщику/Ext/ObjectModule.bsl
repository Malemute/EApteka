
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Дефектура.Записывать = Истина;
	Движения.Дефектура.Очистить();
	Движения.Дефектура.Записать(Истина);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	
	ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Исключая);
	Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПоставщику.Склад КАК Склад,
		|	ЗаказПоставщику.Товар КАК Товар,
		|	ЗаказПоставщику.Количество - ЗаказПоставщику.КоличествоОтказ КАК Количество
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщику
		|ГДЕ
		|	ЗаказПоставщику.Ссылка = &Ссылка
		|	И ЗаказПоставщику.Количество - ЗаказПоставщику.КоличествоОтказ > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Товар,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДефектураОстатки.Склад,
		|	ДефектураОстатки.Товар,
		|	ДефектураОстатки.Заказ,
		|	ДефектураОстатки.КлючСвязи,
		|	ДефектураОстатки.Поставщик,
		|	ДефектураОстатки.ДатаДоставки,
		|	ДефектураОстатки.ЦенаЗакупки,
		|	ДефектураОстатки.СрокГодности,
		|	СУММА(ДефектураОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_ОстаткиДефектуры
		|ИЗ
		|	РегистрНакопления.Дефектура.Остатки(
		|			&МоментВремени,
		|			Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				И Товар В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВТ_Товары.Товар КАК Товар
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)
		|				И Склад В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВТ_Товары.Склад КАК Склад
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК ДефектураОстатки
		|ГДЕ
		|	ДефектураОстатки.КоличествоОстаток > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ДефектураОстатки.Склад,
		|	ДефектураОстатки.Товар,
		|	ДефектураОстатки.Заказ,
		|	ДефектураОстатки.КлючСвязи,
		|	ДефектураОстатки.Поставщик,
		|	ДефектураОстатки.ДатаДоставки,
		|	ДефектураОстатки.СрокГодности,
		|	ДефектураОстатки.ЦенаЗакупки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОстаткиДефектуры.Склад,
		|	ВТ_ОстаткиДефектуры.Товар,
		|	ВТ_ОстаткиДефектуры.Заказ,
		|	ВТ_ОстаткиДефектуры.КлючСвязи,
		|	ВТ_ОстаткиДефектуры.Поставщик,
		|	ВТ_ОстаткиДефектуры.ДатаДоставки,
		|	ВТ_ОстаткиДефектуры.ЦенаЗакупки,
		|	ВТ_ОстаткиДефектуры.СрокГодности,
		|	ВТ_ОстаткиДефектуры.КоличествоОстаток
		|ИЗ
		|	ВТ_ОстаткиДефектуры КАК ВТ_ОстаткиДефектуры";

	ОстаткиДефектуры = Запрос.Выполнить().Выгрузить();
	Запрос.Текст = "
		|
		|ВЫБРАТЬ
		|	ВТ_Товары.Склад,
		|	ВТ_Товары.Товар,
		|	ВТ_Товары.Количество
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары";

	ТабДвижений = Движения.Дефектура.ВыгрузитьКолонки();
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл

		ОсталосьРаспределить = РезультатЗапроса.Количество;
		Для каждого СтрокаТЧ Из ОстаткиДефектуры Цикл
			Если СтрокаТЧ.Склад = РезультатЗапроса.Склад
				И СтрокаТЧ.Товар = РезультатЗапроса.Товар
				И СтрокаТЧ.КоличествоОстаток > 0 Тогда

				текКоличество = Мин(ОсталосьРаспределить, СтрокаТЧ.КоличествоОстаток);
				Движение = ТабДвижений.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Склад = СтрокаТЧ.Склад;
				Движение.Товар = СтрокаТЧ.Товар;
				Движение.Заказ = СтрокаТЧ.Заказ;
				Движение.КлючСвязи = СтрокаТЧ.КлючСвязи;
				Движение.Количество = текКоличество;

				Движение = ТабДвижений.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Склад = СтрокаТЧ.Склад;
				Движение.Товар = СтрокаТЧ.Товар;
				Движение.Заказ = СтрокаТЧ.Заказ;
				Движение.КлючСвязи = СтрокаТЧ.КлючСвязи;
				Движение.Поставщик = Клиент;
				Движение.ДатаДоставки = ДатаДоставки;
				Движение.Количество = текКоличество;

				ОсталосьРаспределить = ОсталосьРаспределить - текКоличество;
				Если ОсталосьРаспределить = 0 Тогда
					Прервать;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла; 

		Если ОсталосьРаспределить > 0 Тогда

			Движение = ТабДвижений.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Склад = РезультатЗапроса.Склад;
			Движение.Товар = РезультатЗапроса.Товар;
			Движение.Поставщик = Клиент;
			Движение.ДатаДоставки = ДатаДоставки;
			Движение.Количество = ОсталосьРаспределить;

		КонецЕсли; 
		
	КонецЦикла; 

	ТабДвижений.Свернуть("ВидДвижения, Период, Склад, Товар, Заказ, КлючСвязи, Поставщик, ДатаДоставки","Количество");
	Движения.Дефектура.Загрузить(ТабДвижений);

КонецПроцедуры

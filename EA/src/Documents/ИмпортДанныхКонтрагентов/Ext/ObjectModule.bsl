
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДанныеПоОстаткам = Остатки.Количество() > 0;
	ДанныеПоЗакупкам = Закупки.Количество() > 0;
	ДанныеПоПродажам = Продажи.Количество() > 0;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Остатки.Количество() > 0 Тогда
		СформироватьДвиженияПоОстаткам();
	КонецЕсли;
	
	Если Закупки.Количество() > 0 Тогда
		СформироватьДвиженияПоЗакупкам();
	КонецЕсли;
	
	Если Продажи.Количество() > 0 Тогда
		СформироватьДвиженияПоПродажам();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Остатки.Количество() <> 0 Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаНачала");
		ПроверяемыеРеквизиты.Добавить("ДатаОкончания");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияПоОстаткам()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101');
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ВложенныйЗапрос.НоменклатураКонтрагента,
	|	ВложенныйЗапрос.Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИмпортДанныхКонтрагентовОстатки.НомерСтроки КАК НомерСтроки,
	|		ИмпортДанныхКонтрагентовОстатки.НоменклатураКонтрагента КАК НоменклатураКонтрагента,
	|		ИмпортДанныхКонтрагентовОстатки.НачальныйОстаток КАК Количество,
	|		ИмпортДанныхКонтрагентовОстатки.Ссылка КАК Регистратор,
	|		ИмпортДанныхКонтрагентовОстатки.Ссылка.ДатаНачала КАК Период
	|	ИЗ
	|		Документ.ИмпортДанныхКонтрагентов.Остатки КАК ИмпортДанныхКонтрагентовОстатки
	|	ГДЕ
	|		ИмпортДанныхКонтрагентовОстатки.Ссылка = &Ссылка
	|		И ИмпортДанныхКонтрагентовОстатки.Ссылка.ДатаНачала <> &ПустаяДата
	|		И ИмпортДанныхКонтрагентовОстатки.НачальныйОстаток <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИмпортДанныхКонтрагентовОстатки.НомерСтроки,
	|		ИмпортДанныхКонтрагентовОстатки.НоменклатураКонтрагента,
	|		ИмпортДанныхКонтрагентовОстатки.КонечныйОстаток,
	|		ИмпортДанныхКонтрагентовОстатки.Ссылка,
	|		ИмпортДанныхКонтрагентовОстатки.Ссылка.ДатаОкончания
	|	ИЗ
	|		Документ.ИмпортДанныхКонтрагентов.Остатки КАК ИмпортДанныхКонтрагентовОстатки
	|	ГДЕ
	|		ИмпортДанныхКонтрагентовОстатки.Ссылка = &Ссылка
	|		И ИмпортДанныхКонтрагентовОстатки.Ссылка.ДатаОкончания <> &ПустаяДата
	|		И ИмпортДанныхКонтрагентовОстатки.КонечныйОстаток <> 0) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатурыКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ОстаткиНоменклатурыКонтрагентов.Записать();
	
КонецПроцедуры

Процедура СформироватьДвиженияПоЗакупкам()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИмпортДанныхКонтрагентовЗакупки.Дата                    КАК Период,
	|	ИмпортДанныхКонтрагентовЗакупки.Ссылка                  КАК Регистратор,
	|	ИмпортДанныхКонтрагентовЗакупки.НомерСтроки             КАК НомерСтроки,
	|	ИмпортДанныхКонтрагентовЗакупки.Ссылка.Контрагент       КАК Контрагент,
	|	ИмпортДанныхКонтрагентовЗакупки.НоменклатураКонтрагента КАК НоменклатураКонтрагента,
	|	ИмпортДанныхКонтрагентовЗакупки.ПоставщикКонтрагента    КАК ПоставщикКонтрагента,
	|	ИмпортДанныхКонтрагентовЗакупки.Количество              КАК Количество,
	|	ИмпортДанныхКонтрагентовЗакупки.Сумма                   КАК Сумма
	|ИЗ
	|	Документ.ИмпортДанныхКонтрагентов.Закупки КАК ИмпортДанныхКонтрагентовЗакупки
	|ГДЕ
	|	ИмпортДанныхКонтрагентовЗакупки.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ЗакупкиКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ЗакупкиКонтрагентов.Записать();
	
КонецПроцедуры

Процедура СформироватьДвиженияПоПродажам()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИмпортДанныхКонтрагентовПродажи.Дата КАК Период,
	|	ИмпортДанныхКонтрагентовПродажи.Ссылка КАК Регистратор,
	|	ИмпортДанныхКонтрагентовПродажи.НомерСтроки КАК НомерСтроки,
	|	ИмпортДанныхКонтрагентовПродажи.Ссылка.Контрагент,
	|	ИмпортДанныхКонтрагентовПродажи.НоменклатураКонтрагента,
	|	ИмпортДанныхКонтрагентовПродажи.Количество,
	|	ИмпортДанныхКонтрагентовПродажи.СуммаЗакупки,
	|	ИмпортДанныхКонтрагентовПродажи.СуммаПродажи
	|ИЗ
	|	Документ.ИмпортДанныхКонтрагентов.Продажи КАК ИмпортДанныхКонтрагентовПродажи
	|ГДЕ
	|	ИмпортДанныхКонтрагентовПродажи.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ПродажиКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ПродажиКонтрагентов.Записать();
	
КонецПроцедуры

#КонецОбласти
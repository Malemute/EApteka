
&НаКлиенте
Процедура ПослеВводаСрока(Дата, Параметры) Экспорт
	
	Если не дата = неопределено тогда
		Если ЗначениеЗаполнено(Дата) тогда
			ЗаполнитьПеремещениеПоСрокамХранения(Дата);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСроковому(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаСрока",ЭтаФорма);	
	ПоказатьВводДаты(Оповещение
	,
	,"Введите дату срока хранения.
	|После ввода даты табличная часть будет очищенна!"
	,ЧастиДаты.Дата);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПеремещениеПоСрокамХранения(Дата)
	
	Запрос = Новый Запрос;	
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	ОстаткиТовараОстатки.Товар,
	|	ОстаткиТовараОстатки.Партия,
	|	ОстаткиТовараОстатки.ОстатокОстаток КАК Количество,
	|	ОстаткиТовараОстатки.Партия.ГоденДо КАК ПартияГоденДо
	|ИЗ
	|	РегистрНакопления.ОстаткиТовара.Остатки(
	|			,
	|			Отдел = &Отдел
	|				И Партия.ГоденДо <= &СрокГодности
	|				И Партия.ГоденДо <> ДАТАВРЕМЯ(1, 1, 1)) КАК ОстаткиТовараОстатки
	|ГДЕ
	|	ОстаткиТовараОстатки.ОстатокОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПартияГоденДо";
	Запрос.УстановитьПараметр("СрокГодности",Дата);
	Запрос.УстановитьПараметр("Отдел",Объект.Поставщик);
	
	Объект.Товары.Очистить();
	
	ТЗОстатки = Запрос.Выполнить().Выгрузить();
	Если Объект.СборкаНаТСД тогда
		ТЗОстатки.Колонки.Количество.Имя = "КоличествоПлан";
	КонецЕсли;
	
	Объект.Товары.Загрузить(ТЗОстатки);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрыВыбораМассив = Новый Массив;
	Если ЗначениеЗаполнено(ПараметрыСеанса.ТекущийАдресХранения) тогда
		ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора("Отбор.Владелец",ПараметрыСеанса.ТекущийАдресХранения));
	КонецЕсли;
	ПараметрыВыбораФиксированныйМассив = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
	Элементы.Поставщик.ПараметрыВыбора = ПараметрыВыбораФиксированныйМассив;
	УстановитьДоступность(Истина);	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность(Открытие = Ложь)
	Если Объект.ВводЗавершен тогда
		Если Открытие тогда
			Элементы.ВводЗавершен.Доступность = Ложь;
		КонецЕсли;
		Элементы.СборкаНаТСД.Доступность = Ложь;
		Элементы.Товары.ТолькоПросмотр = Истина;
		Элементы.ТоварыЗаполнитьПоСроковому.Доступность = Ложь;
		Элементы.ТоварыЗаполнитьИзExcel.Доступность = Ложь;
		Элементы.ТоварыЗаполнитьНоменклатуруВнеМатрицы.Доступность = Ложь;
	Иначе
		Элементы.СборкаНаТСД.Доступность = Истина;
		Элементы.Товары.ТолькоПросмотр = Ложь;
		Элементы.ТоварыЗаполнитьПоСроковому.Доступность = Истина;
		Элементы.ТоварыЗаполнитьИзExcel.Доступность = Истина;
		Элементы.ТоварыЗаполнитьНоменклатуруВнеМатрицы.Доступность = Истина;
	КонецЕсли;
	Если Объект.СборкаНаТСД тогда
		Элементы.ТоварыКоличествоПлан.Видимость = Истина;
	Иначе
		Элементы.ТоварыКоличествоПлан.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ВводЗавешенПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура СборкаНаТСДПриИзменении(Элемент)	
	СборкаНаТСДПриИзмененииНаСервере();
	УстановитьДоступность();
КонецПроцедуры

&НаСервере
Процедура СборкаНаТСДПриИзмененииНаСервере()
	Для каждого строка из Объект.Товары цикл
		Если Объект.СборкаНаТСД тогда
			Строка.КоличествоПлан = Строка.Количество;
			Строка.Количество = 0 ;
		Иначе
			Строка.Количество = Строка.КоличествоПлан;
			Строка.КоличествоПлан = 0;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзExcelНаСервере(МассивСтрок)
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("КодТовара",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10)));
	ТаблицаТоваров.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,0)));
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10,0)));
	Для Каждого строка из МассивСтрок цикл
		СтрокаТЗ = ТаблицаТоваров.Добавить();
		СтрокаТЗ.КодТовара = строка.КодТовара;
		СтрокаТЗ.Количество = строка.Количество;
		СтрокаТЗ.НомерСтроки = строка.НомерСтроки;  
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товар.КодТовара,
	|	Товар.Количество,
	|	Товар.НомерСтроки
	|ПОМЕСТИТЬ втДанныеExcel
	|ИЗ
	|	&Товар КАК Товар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеExcel.КодТовара КАК КодТовара,
	|	втДанныеExcel.Количество КАК Количество,
	|	втДанныеExcel.НомерСтроки КАК НомерСтроки,
	|	Номенклатура.Ссылка КАК Товар,
	|	ОстаткиТовараОстатки.Партия,
	|	ЕСТЬNULL(ОстаткиТовараОстатки.ОстатокОстаток, 0) КАК Остаток,
	|	ОстаткиТовараОстатки.Партия.ГоденДо КАК ПартияГоденДо,
	|	Естьnull(Номенклатура.КраткоеНаименование,"""") КАК КраткоеНаименование
	|ИЗ
	|	втДанныеExcel КАК втДанныеExcel
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТовара.Остатки(, Отдел = &Отдел) КАК ОстаткиТовараОстатки
	|			ПО Номенклатура.Ссылка = ОстаткиТовараОстатки.Товар
	|		ПО втДанныеExcel.КодТовара = Номенклатура.Код
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПартияГоденДо
	|ИТОГИ
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(НомерСтроки),
	|	МАКСИМУМ(КраткоеНаименование)
	|ПО
	|	КодТовара,
	|	Товар";
	Запрос.УстановитьПараметр("Отдел",Объект.Поставщик);                            
	Запрос.УстановитьПараметр("Товар",ТаблицаТоваров);
	ВыборкаКод = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКод.Следующий() цикл
		Количество = ВыборкаКод.Количество;
		НомерСтроки = ВыборкаКод.НомерСтроки;
		ВыборкаТовар = ВыборкаКод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаТовар.Следующий() цикл
			Если ЗначениеЗаполнено(ВыборкаТовар.Товар) тогда
				Выборка = ВыборкаТовар.Выбрать();
				Пока Выборка.Следующий() цикл
					БудетСписано = Мин(Количество,Выборка.Остаток);
					Если БудетСписано = 0 тогда
						Прервать;
					КонецЕсли;
					Количество = Количество - БудетСписано;
					Строка = Объект.Товары.Добавить();
					Строка.Товар = Выборка.Товар;
					Строка.Партия = Выборка.Партия;
					Строка.Количество = БудетСписано;
					Если Количество = 0 тогда
						Прервать
					КонецЕсли;
				КонецЦикла;
			Иначе
				Сообщение = новый СообщениеПользователю;
				Сообщение.Текст = "Не найден товар по коду в строке " + Строка(НомерСтроки);
				Сообщение.Сообщить();
				Прервать;
			КонецЕсли;     				
			Если Количество > 0 тогда
				Сообщение = новый СообщениеПользователю;
				Сообщение.Текст = "По строке "+Выборка.НомерСтроки+" ("+ ВыборкаТовар.КраткоеНаименование +") не хватает "+Количество;
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзExcel(Команда)
	
	Объект.Товары.Очистить();
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл";
	Диалог.ПолноеИмяФайла = "";
	Фильтр = "XLS (*.xls), XLSX (*.xlsx)|*.xls; *.xlsx"; 
	Диалог.Фильтр = Фильтр; 
	Диалог.МножественныйВыбор = Ложь;
	Если Диалог.Выбрать() Тогда
		ПутьКФайлу = Диалог.ПолноеИмяФайла;
	Иначе
		Возврат;
	КонецЕсли;	
	
	Excel = Новый COMОбъект("Excel.Application");
	Excel.Visible       = Ложь;
	Excel.DisplayAlerts = Ложь;
	Книга = Excel.Workbooks.Open(ПутьКФайлу, , Истина);
	Лист = Книга.Sheets(1);
	КолвоКолонок = Лист.Cells(1,1).SpecialCells(11).Column;
	КолвоСтрок   = Лист.Cells(1,1).SpecialCells(11).Row;
	МассивСтрок = Новый Массив;
	
	Для НС = 1 по КолвоСтрок цикл
		
		
		КодТовара = Формат(Лист.Cells(НС,1).Value,"ЧГ=0");
		Количество = Лист.Cells(НС,3).Value;
		
		
		ДанныеКорректны = Истина;
		
		Если НЕ ЗначениеЗаполнено(КодТовара) тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "В строке "+Строка(НС)+" не заполнен код товара!";
			Сообщение.Сообщить();
			ДанныеКорректны = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Количество) тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "В строке "+Строка(НС)+" не заполнено количество!";
			Сообщение.Сообщить();
			ДанныеКорректны = Ложь;
		Иначе
			Попытка
				Количество = Число(Количество);
			Исключение
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "В строке "+Строка(НС)+" количествор не число!";
				Сообщение.Сообщить();
				ДанныеКорректны = Ложь;	
			КонецПопытки;
		КонецЕсли;
				
		Если ДанныеКорректны тогда
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("КодТовара",КодТовара);
			СтруктураСтроки.Вставить("Количество",Количество);
			СтруктураСтроки.Вставить("НомерСтроки",НС);
			МассивСтрок.Добавить(СтруктураСтроки);		
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьИзExcelНаСервере(МассивСтрок);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуруВнеМатрицыНаСервере()
	Объект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиТовараОстатки.Товар,
	|	ОстаткиТовараОстатки.Партия,
	|	ОстаткиТовараОстатки.ОстатокОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ОстаткиТовара.Остатки(, Отдел = &Отдел) КАК ОстаткиТовараОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПрайсМестаХранения КАК ПрайсМестаХранения
	|		ПО ОстаткиТовараОстатки.Товар = ПрайсМестаХранения.Владелец.Товар
	|			И ОстаткиТовараОстатки.Отдел = ПрайсМестаХранения.Отдел
	|ГДЕ
	|	(НЕ ПрайсМестаХранения.ДержимНаСкладе
	|			ИЛИ ПрайсМестаХранения.Ссылка ЕСТЬ NULL )
	|	И НЕ ПрайсМестаХранения.ПометкаУдаления";
	Запрос.УстановитьПараметр("Отдел",Объект.Поставщик);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() цикл
		Строка = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строка,Выборка);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуруВнеМатрицы(Команда)
	ЗаполнитьНоменклатуруВнеМатрицыНаСервере();
КонецПроцедуры

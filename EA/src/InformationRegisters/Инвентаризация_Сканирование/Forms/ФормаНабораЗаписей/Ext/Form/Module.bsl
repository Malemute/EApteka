
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Склад = ПараметрыСеанса.ОсновнойСклад;
	
	НаборЗаписей.Отбор.Склад.Значение = Склад; 
	НаборЗаписей.Отбор.Склад.Использование = Истина;
	
	НаборЗаписей.Отбор.Сотрудник.Значение = Сотрудник; 
	НаборЗаписей.Отбор.Сотрудник.Использование = Истина;
	
	ПрочитатьНЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Источник = "BarCodeScaner" тогда
		
		Если Событие = "BarCodeValue" тогда
			
			Scan.ПосылкаДанных = 1;   
			Ответ = ЗаполнитьСотрудника(Данные);
			
			Если ТипЗнч(Ответ) = Тип("Строка") тогда
				ПоказатьПредупреждение(,Ответ);
			Иначе
				Элементы.НаборЗаписей.ТолькоПросмотр = Ложь;
			КонецЕсли;
			
			ПрочитатьНЗ();
			Элементы.НаборЗаписей.Обновить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция ЗаполнитьСотрудника(Данные)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ШтрихКод = &ШтрихКод";
	Запрос.УстановитьПараметр("ШтрихКод", Данные);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() тогда
		Возврат "Сотрудник не найден!";
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() цикл
		Сотрудник = Выборка.Ссылка;
	КонецЦикла;
	
	НаборЗаписей.Отбор.Сотрудник.Значение = Сотрудник; 
	НаборЗаписей.Отбор.Сотрудник.Использование = Истина;
	
	ПрочитатьНЗ();
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура Склад1ПриИзменении(Элемент)
	
	НаборЗаписей.Отбор.Склад.Значение = Склад; 
	НаборЗаписей.Отбор.Склад.Использование = Истина;
	
	ПрочитатьНЗ();
	
	Элементы.НаборЗаписей.Обновить();

КонецПроцедуры


&НаСервере
Процедура ПрочитатьНЗ()
	
	НЗ = РеквизитФормыВЗначение("НаборЗаписей");
	
	НЗ.Прочитать();
	
	ЗначениеВРеквизитФормы(НЗ,"НаборЗаписей");
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока тогда
		Элементы.НаборЗаписей.ТекущиеДанные.Период = НачалоДня(ТекущаяДата());
		Элементы.НаборЗаписей.ТекущиеДанные.Сотрудник = Сотрудник;
		Элементы.НаборЗаписей.ТекущиеДанные.Склад = Склад;
	КонецЕсли;
КонецПроцедуры



&НаКлиенте
Процедура ТоварОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	Если ТекущиеДанные <> неопределено тогда
		МассивСтрок = НаборЗаписей.НайтиСтроки(Новый Структура("Товар",ВыбранноеЗначение));	
		Если МассивСтрок.Количество() > 0 тогда
			Если Не ЗначениеЗаполнено(ТекущиеДанные.Товар) тогда
				НаборЗаписей.Удалить(ТекущиеДанные);
				Элементы.НаборЗаписей.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
			Иначе
				Если Вопрос("Выбранный товар уже есть в табличной части.
					|Удалить текущую строку и перейти к строке с товаром?",РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да тогда
					НаборЗаписей.Удалить(ТекущиеДанные);
					Элементы.НаборЗаписей.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
					Элементы.НаборЗаписей.ИзменитьСтроку();
				Иначе
					ВыбранноеЗначение = ТекущиеДанные.Товар;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;                              
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Вопрос("Закрыть форму?",РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры



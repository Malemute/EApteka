
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		Контракт = ОпределитьТекущийКонтракт();
		Если Контракт = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не установлен отбор по контракту.'"));
			Возврат;
		КонецЕсли;
		
		ЗаполнитьНоменклатуруКонтракта(Контракт, ВыбранноеЗначение.АдресТоваровВХранилище);
		
		ПоказатьПредупреждение(, НСтр("ru='Заполнение номенклатуры контракта завершено.'"));
		
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗаполнитьНоменклатуруПоОтбору(Команда)
	
	ОткрытьФорму("РегистрСведений.НоменклатураКонтракта.Форма.ЗаполнениеНоменклатурыПоОтбору",, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНоменклатуруИзВнешнегоФайла(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗагружатьМинимальныйЗапас", Истина);
	ПараметрыФормы.Вставить("ЗагружатьУчитыватьНаличие", Истина);
	
	ОткрытьФорму("Обработка.ЗагрузкаДанныхИзВнешнихФайлов.Форма.Форма", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНоменклатуруКонтракта(Команда)
	
	Контракт = ОпределитьТекущийКонтракт();
	Если Контракт = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не установлен отбор по контракту.'"));
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Контракт", Контракт);
	
	ТекстСообщения = НСтр("ru='Номенклатура контракта ""%1"" будет очищена. Продолжить?'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(Контракт));
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьНоменклатуруКонтракта_Завершение", ЭтотОбъект, ДополнительныеПараметры), ТекстСообщения, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьНоменклатуруКонтракта(Контракт, АдресТоваровВХранилище)
	
	ТаблицаДанных = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Нет данных для заполнения.'");
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НоменклатураКонтракта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Контракт.Установить(Контракт);
	НаборЗаписей.Прочитать();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Номенклатура = СтрокаТаблицы.Номенклатура;
		Запись.Контракт = Контракт;
		Запись.УчитыватьНаличие = СтрокаТаблицы.УчитыватьНаличие;
		Запись.МинимальныйЗапас = СтрокаТаблицы.МинимальныйЗапас;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиенте
Функция ОпределитьТекущийКонтракт()
	
	Результат = Неопределено;
	
	МассивЭлементов = КомпоновкаДанныхКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.Отбор, "Контракт");
	
	Для Каждого ЭлементОтбора Из МассивЭлементов Цикл
		Если ЭлементОтбора.Использование И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			Результат = ЭлементОтбора.ПравоеЗначение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОчиститьНоменклатуруКонтракта_Завершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьНоменклатуруКонтрактаНаСервере(ДополнительныеПараметры.Контракт);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьНоменклатуруКонтрактаНаСервере(Контракт)
	
	НаборЗаписей = РегистрыСведений.НоменклатураКонтракта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Контракт.Установить(Контракт);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти
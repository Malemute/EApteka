

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// проверим дублирование в тч ВидыОбъектов
	КопТабЧастиВидыОбъектов = ЭтотОбъект.ВидыОбъектов.Выгрузить( , "ВидОбъекта" );
	КопТабЧастиВидыОбъектов.Свернуть( "ВидОбъекта", "" );
	
	Если КопТабЧастиВидыОбъектов.Количество() <> ЭтотОбъект.ВидыОбъектов.Количество() Тогда
		ОбменДаннымиСервер.СообщитьОбОшибке( "В таб. части ""Виды Объектов"" есть повторяющиеся значения Вида Объекта. Дублирования Вида Объекта недопустимо.", Отказ );
	КонецЕсли;
	
	// проверим на наличие Узлов, относящихся к текущей базе
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр( "ТаблицаУзлов", ЭтотОбъект.УзлыПлановОбмена.Выгрузить( , "НомерСтроки, Узел" ) );
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаУзлов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУзлов.Узел КАК Узел
	|ПОМЕСТИТЬ табУзлов
	|ИЗ
	|	&ТаблицаУзлов КАК ТаблицаУзлов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	табУзлов.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	табУзлов КАК табУзлов
	|ГДЕ
	|	табУзлов.Узел.ЭтотУзел = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбменДаннымиСервер.СообщитьОбОшибке( "В строке " + Выборка.НомерСтроки + " таб. части ""Узлы планов обмена"" указан узел, относящийся к текущей базе.", Отказ );
	КонецЦикла;
	
	
КонецПроцедуры



&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьКомпоновщикНастроекНаСервере( Истина );
	
КонецПроцедуры

&НаСервере
Функция ИнициализироватьКомпоновщикНастроекНаСервере( ПрочитатьНастройки = Ложь )
	
	СформироватьИнициализироватьСхемуКомпоновкиДанных();
	
	Если ПрочитатьНастройки = Истина Тогда
	
		Попытка
			КомпоновщикНастроек.ЗагрузитьНастройки( РеквизитФормыВЗначение( "Объект" ).НастройкиОтбора.Получить() );
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	КомпоновщикНастроек.Восстановить();
	
КонецФункции

&НаСервере
Процедура СформироватьИнициализироватьСхемуКомпоновкиДанных()
	
	СхемаКомпоновкиДанных = Неопределено;
	
	Если ЗначениеЗаполнено( Объект.ВидОбъекта ) Тогда
		
		СхемаКомпоновкиДанных = СвязиИОтборы.СформироватьСхемуКомпоновкиДанныхПоВидуОбъектаИсточникаУсловий_НаборДанныхОбъект( Объект.ВидОбъекта );
		
		Если СхемаКомпоновкиДанных = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю( "Ошибка при компоновке доступных полей. Выбран не корректный элемент Вид Объект.", Объект.ВидОбъекта, "ВидОбъекта", "Объект.ВидОбъекта" );
		КонецЕсли;
		
	КонецЕсли;
	
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;
	КонецЕсли;
	
	Если АдресВременнохоХранилищаКомпоновщикаНастроек = "" Тогда
		АдресВременнохоХранилищаКомпоновщикаНастроек = ПоместитьВоВременноеХранилище( СхемаКомпоновкиДанных, УникальныйИдентификатор );
	Иначе
		АдресВременнохоХранилищаКомпоновщикаНастроек = ПоместитьВоВременноеХранилище( СхемаКомпоновкиДанных, АдресВременнохоХранилищаКомпоновщикаНастроек );
	КонецЕсли;
	
	КомпоновщикНастроек.Инициализировать( Новый ИсточникДоступныхНастроекКомпоновкиДанных( АдресВременнохоХранилищаКомпоновщикаНастроек ) );
	КомпоновщикНастроек.ЗагрузитьНастройки( СхемаКомпоновкиДанных.НастройкиПоУмолчанию );
	КомпоновщикНастроек.Восстановить();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено( ТекущийОбъект.ВидОбъекта ) Тогда
		ТекущийОбъект.НастройкиОтбора = Новый ХранилищеЗначения( КомпоновщикНастроек.ПолучитьНастройки(), Новый СжатиеДанных(9) );
	Иначе
		ТекущийОбъект.НастройкиОтбора = Новый ХранилищеЗначения( Неопределено );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОбъектаПриИзменении(Элемент)
	
	ВидОбъектаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВидОбъектаПриИзмененииНаСервере()
	
	ИнициализироватьКомпоновщикНастроекНаСервере();
	
КонецПроцедуры



Функция ОтправитьЭлектронноеПисьмо(УчетнаяЗапись,ТипТекста,Тема,Получатель,Вложение,ТелоПисьма)  Экспорт
	Почта = ПодключитсяКЭлектроннойПочтеПоУчетнойЗаписи(УчетнаяЗапись);
	Письмо = Новый ИнтернетПочтовоеСообщение;
	Письмо.Тексты.Добавить(ТелоПисьма,ТипТекста);
	Письмо.Тема = Тема;
	Письмо.ИмяОтправителя = УчетнаяЗапись.ИмяПользователя;
	МассивПолучателей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Получатель,";",Истина,Истина);
	Для каждого АдресПолучателя из МассивПолучателей цикл
		Письмо.Получатели.Добавить(АдресПолучателя);
	КонецЦикла;
	Письмо.Отправитель = УчетнаяЗапись.АдресЭлектроннойПочты;
	Если ТипЗнч(Вложение) = Тип("Массив") тогда
		Для каждого Файл из Вложение цикл
			Письмо.Вложения.Добавить(Файл);
		КонецЦикла;
	ИначеЕсли ТипЗнч(Вложение) = Тип("Строка") и ЗначениеЗаполнено(Вложение) тогда
		Письмо.Вложения.Добавить(Вложение);
	КонецЕсли;
	Попытка
		Почта.Послать(Письмо);
	Исключение
		Ошибка = ОписаниеОшибки();
		Почта.Отключиться(); 
		ВызватьИсключение Ошибка;
	КонецПопытки;
	
	Почта.Отключиться();
	Письмо.Вложения.Очистить();	
	
КонецФункции

Функция ПодключитсяКЭлектроннойПочтеПоУчетнойЗаписи(УчетнаяЗапись) Экспорт
	
	//УчетнаяЗапись = справочники.УчетныеЗаписиЭлектроннойПочты.СоздатьЭлемент();
	Профиль = Новый ИнтернетПочтовыйПрофиль;
	Профиль.АдресСервераIMAP = УчетнаяЗапись.АдресСервераIMAP;
	Профиль.АдресСервераSMTP = УчетнаяЗапись.АдресСервераSMTP;
	Профиль.ПортIMAP = УчетнаяЗапись.ПортIMAP;
	Профиль.ПортSMTP = УчетнаяЗапись.ПортSMTP;
	Профиль.ИспользоватьSSLIMAP = УчетнаяЗапись.ИспользоватьSSLIMAP;
	Профиль.ИспользоватьSSLSMTP = УчетнаяЗапись.ИспользоватьSSLSMTP;
	Профиль.Пользователь = УчетнаяЗапись.АдресЭлектроннойПочты;
	Профиль.ПарольIMAP = УчетнаяЗапись.ПарольIMAP;
	Профиль.ПарольSMTP = УчетнаяЗапись.ПарольSMTP;
	Профиль.ПользовательSMTP = УчетнаяЗапись.ПользовательSMTP;
	Профиль.ПользовательIMAP = УчетнаяЗапись.ПользовательIMAP;
	Профиль.Таймаут = УчетнаяЗапись.Таймаут;
	Профиль.АутентификацияSMTP = СпособSMTPАутентификации.Plain;
	
	Попытка
		Почта = Новый ИнтернетПочта;
		Почта.Подключиться(Профиль);
		Возврат Почта;
	Исключение
		Ошибка = ОписаниеОшибки();
		ВызватьИсключение Ошибка;
		Возврат неопределено;
	КонецПопытки;
	
	
КонецФункции


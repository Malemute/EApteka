
// Функция - Получить ссылку описателя типа по идентификатору
//     Возвращает ссылку описателя типа для информационной базы по идентификатору, если подходящего нет, то будет создан новый
//
// Параметры:
//  ИнформационнаяБаза	 - СправочникСсылка.ИнформационныеБазы - информационная база, владелец типа
//  ИдентификаторТипа	 - Строка - идентификатор типа информационной базы
// 
// Возвращаемое значение:
//   - СправочникСсылка.ОписателиТиповИнформационныхБаз - полученная ссылка описателя типа
//
Функция ПолучитьСсылкуОписателяТипаПоИдентификатору( ИнформационнаяБаза, ИдентификаторТипа ) Экспорт
	
	УстановитьПривилегированныйРежим( Истина );
	
	Возврат Справочники.ОписателиТиповИнформационныхБаз.ПолучитьСсылкуОписателяТипаПоИдентификатору( ИнформационнаяБаза, ИдентификаторТипа );
	
КонецФункции

// Функция - Текущая база не является копией
//     Проверяет факт того, что текущая база является копией
//     Для проверки используется сопоставление стротроки соединения с насройкой элемента ТекущаяБаза в справочнике ИнформационныеБазы
// 
// Возвращаемое значение:
//   - Булево - Истина, если это копия
//
Функция ТекущаяБазаЯвляетсяКопией() Экспорт
	
	УстановитьПривилегированныйРежим( Истина );
	
	ТекущаяСтрокаСоединения = ВРег( СтрокаСоединенияИнформационнойБазы() );
	ЭталоннаяСтрокаСоединения = ВРег( СокрЛП( Справочники.ИнформационныеБазы.ТекущаяБаза.Комментарий ) );
	
	Возврат ( ТекущаяСтрокаСоединения <> ЭталоннаяСтрокаСоединения );
	
КонецФункции

//>>Хортюк С.Б. 20170925
Функция ПолучитьТекстЗапроса_КоличествоОбъектовВОчередиПоУзлуПланаОбмена(ПланОбменаИмя,УсловиеНаУзел = Неопределено,ПоМетаданным = Ложь,Дерево = Ложь) Экспорт
	
	ТекстЗапроса = "";
	
	СоставПланаОбмена = Метаданные.ПланыОбмена[ПланОбменаИмя].Состав;
	
	Количество = СоставПланаОбмена.Количество();
	ИТ = 0;
	Для каждого Тип из СоставПланаОбмена цикл
		ИТ = ИТ + 1;
		Метаданное = Тип.Метаданные;
		ТипМетаданного = "";
		СтрокаКоличество = "";
		Если Метаданные.Справочники.Содержит(Метаданное) тогда
			ТипМетаданного = "Справочник";
			ТипМетаданногоВыборка = "Справочники";
			СтрокаКоличество = "КОЛИЧЕСТВО(Таблица.Ссылка) КАК Количество";
		ИначеЕсли Метаданные.Документы.Содержит(Метаданное) тогда
			ТипМетаданного = "Документ";
			ТипМетаданногоВыборка = "Документы";
			СтрокаКоличество = "КОЛИЧЕСТВО(Таблица.Ссылка) КАК Количество";
		ИначеЕсли Метаданные.РегистрыСведений.Содержит(Метаданное) тогда
			ТипМетаданного = "РегистрСведений";
			ТипМетаданногоВыборка = "РегистрыСведений";
			СтрокаКоличество = "КОЛИЧЕСТВО(Таблица.Узел) КАК Количество";
		ИначеЕсли Метаданные.РегистрыНакопления.Содержит(Метаданное) тогда
			ТипМетаданного = "РегистрНакопления";
			ТипМетаданногоВыборка = "РегистрыНакопления";
			СтрокаКоличество = "КОЛИЧЕСТВО(Таблица.Узел) КАК Количество";
		Иначе
			Продолжить;
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + 
		"
		|ВЫБРАТЬ
		|	Таблица.Узел,
		|	"+СтрокаКоличество +
		?(ПоМетаданным,
		"
		|   ,"""+ТипМетаданногоВыборка+""" как ТипМетаданного
		|   ,"""+Метаданное.Имя+""" как ИмяМетаданного
		|",
		"")+
		?(ИТ = 1,
		"
		|ПОМЕСТИТЬ вт
		|",
		"")+
		"
		|ИЗ
		|	"+ТипМетаданного+"."+Метаданное.Имя+".Изменения КАК Таблица
		|
		|ГДЕ
		|	Таблица.Узел ссылка ПланОбмена." + ПланОбменаИмя + 
		?(ТипЗнч(УсловиеНаУзел) = Тип("Строка") и ЗначениеЗаполнено(УсловиеНаУзел)
		,"
		|	И ("+УсловиеНаУзел+")
		|",
		"")+
		"
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Узел"
		+?(ИТ <> Количество,
		"
		|ОБЪЕДИНИТЬ ВСЕ
		|",
		"
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	""                                                                      "",
		|	0"
		+?(ПоМетаданным,
		"
		|	,""""
		|	,""""
		|",
		""));
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса +
	";
	|ВЫБРАТЬ
	|	вт.Узел,
	|	СУММА(вт.Количество) КАК Количество"
	+ ?(ПоМетаданным,
	"
	|	,вт.ТипМетаданного
	|	,вт.ИмяМетаданного
	|",
	"")+
	"	
	|ИЗ
	|	вт КАК вт
	|ГДЕ
	|	 ТИПЗНАЧЕНИЯ(вт.Узел) <> ТИП(СТРОКА)
    |
	|СГРУППИРОВАТЬ ПО
	|	вт.Узел"
	+ ?(ПоМетаданным,
	"
	|	,вт.ТипМетаданного
	|	,вт.ИмяМетаданного
	|",
	"")+
	?(ПоМетаданным и Дерево,
	"
	|ИТОГИ
	|	СУММА(Количество) 
	|ПО
	|	Узел,
	|   ТипМетаданного
	|",
	"")
	;
	
	Возврат ТекстЗапроса
	
	
КонецФункции
//<<Хортюк С.Б. 20170925


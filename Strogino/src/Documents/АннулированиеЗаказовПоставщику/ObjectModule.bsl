
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВиддвиженияНакопления.Расход) КАК ВидДвижения,
		|	АннулированиеЗаказовПоставщику.Ссылка.Дата КАК Период,
		|	АннулированиеЗаказовПоставщику.Ссылка.Склад,
		|	АннулированиеЗаказовПоставщику.Поставщик,
		|	АннулированиеЗаказовПоставщику.Товар,
		|	АннулированиеЗаказовПоставщику.Заказ,
		|	АннулированиеЗаказовПоставщику.КлючСвязи,
		|	АннулированиеЗаказовПоставщику.ДатаДоставки,
		|	АннулированиеЗаказовПоставщику.ЦенаЗакупки,
		|	АннулированиеЗаказовПоставщику.СрокГодности,
		|	АннулированиеЗаказовПоставщику.Количество,
		|	АннулированиеЗаказовПоставщику.НомерСтроки
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.АннулированиеЗаказовПоставщику.Товары КАК АннулированиеЗаказовПоставщику
		|ГДЕ
		|	АннулированиеЗаказовПоставщику.Ссылка = &Ссылка
		|	И АннулированиеЗаказовПоставщику.Количество > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.ВидДвижения,
		|	ВТ_Товары.Период,
		|	ВТ_Товары.Склад,
		|	ВТ_Товары.Поставщик,
		|	ВТ_Товары.Товар,
		|	ВТ_Товары.Заказ,
		|	ВТ_Товары.КлючСвязи,
		|	ВТ_Товары.ДатаДоставки,
		|	ВТ_Товары.ЦенаЗакупки,
		|	ВТ_Товары.СрокГодности,
		|	ВТ_Товары.Количество
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВиддвиженияНакопления.Приход),
		|	ВТ_Товары.Период,
		|	ВТ_Товары.Склад,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
		|	ВТ_Товары.Товар,
		|	ВТ_Товары.Заказ,
		|	ВТ_Товары.КлючСвязи,
		|	NULL,
		|	0,
		|	NULL,
		|	ВТ_Товары.Количество
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары";

	Движения.Дефектура.Записывать = Истина;
	Движения.Дефектура.Загрузить(Запрос.Выполнить().Выгрузить());
	Движения.Записать();

	// Проверим образовался ли отрицательный остаток
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТ_Товары.НомерСтроки КАК НомерСтроки,
		|	ВТ_Товары.Товар КАК Товар,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТ_Товары.Товар) КАК ТоварПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТ_Товары.Поставщик) КАК ПоставщикПредставление,
		|	-Остатки.КоличествоОстаток КАК Дефецит
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Дефектура.Остатки(
		|				&МоментВремени,
		|				Товар В
		|						(ВЫБРАТЬ
		|							ВТ_Товары.Товар КАК Товар
		|						ИЗ
		|							ВТ_Товары КАК ВТ_Товары)
		|					И Поставщик В
		|						(ВЫБРАТЬ
		|							ВТ_Товары.Поставщик КАК Поставщик
		|						ИЗ
		|							ВТ_Товары КАК ВТ_Товары)) КАК Остатки
		|		ПО ВТ_Товары.Поставщик = Остатки.Поставщик
		|			И ВТ_Товары.Товар = Остатки.Товар
		|			И ВТ_Товары.Заказ = Остатки.Заказ
		|			И ВТ_Товары.КлючСвязи = Остатки.КлючСвязи
		|			И ВТ_Товары.ДатаДоставки = Остатки.ДатаДоставки
		|			И ВТ_Товары.ЦенаЗакупки = Остатки.ЦенаЗакупки
		|			И ВТ_Товары.СрокГодности = Остатки.СрокГодности
		|ГДЕ
		|	Остатки.КоличествоОстаток < 0";

		ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
		Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда

			Отказ = Истина;
			ВыборкаОшибки = РезультатЗапроса.Выбрать();
			Пока ВыборкаОшибки.Следующий() Цикл
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Строка " + ВыборкаОшибки.НомерСтроки + " - " + ВыборкаОшибки.ТоварПредставление + ", поставщик: " + ВыборкаОшибки.ПоставщикПредставление + " - недостаточно в количестве " + ВыборкаОшибки.Дефецит + " шт.";
				Сообщение.Сообщить();
			КонецЦикла;

		КонецЕсли;


КонецПроцедуры

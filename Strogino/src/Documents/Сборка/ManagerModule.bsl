Функция СоздатьКомплектациюЗаказаНаОснованииСборкиЗаказа(СборкаЗаказаСсылка,Сотрудник,СсылкаДокументКомплектация) Экспорт
	
	РаспределениеЗСЯ = РаспределитьЗаказыСборкиПоЗСЯ(СборкаЗаказаСсылка);
	
	Если ТипЗнч(РаспределениеЗСЯ) = Тип("Строка") тогда
		Возврат РаспределениеЗСЯ;
	КонецЕсли;
	
	ДокументКомплектация = Документы.КомплектацияЗаказов.СоздатьДокумент();
	
	ДокументКомплектация.Дата = ТекущаяДата();
	ДокументКомплектация.ДокументОснование = СборкаЗаказаСсылка;
	ДокументКомплектация.Сотрудник = Сотрудник;
	ДокументКомплектация.Холод = СборкаЗаказаСсылка.Холод;
	ДокументКомплектация.Склад = СборкаЗаказаСсылка.Склад;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаспределениеЗСЯ.ЗСЯ,
	|	РаспределениеЗСЯ.Заказ
	|ПОМЕСТИТЬ РаспределениеЗСЯ
	|ИЗ
	|	&РаспределениеЗСЯ КАК РаспределениеЗСЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаЗаказовТовары.Документ как Заказ,
	|	СборкаЗаказовТовары.Товар,
	|	СборкаЗаказовТовары.Партия,
	|	СборкаЗаказовТовары.КоличествоСобрано КАК Количество,
	|	РаспределениеЗСЯ.ЗСЯ КАК ЗСЯ,
	|	СборкаЗаказовТовары.КоличествоНеХватило как НеХватило
	|ИЗ
	|	Документ.Сборка.Товары КАК СборкаЗаказовТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределениеЗСЯ КАК РаспределениеЗСЯ
	|		ПО СборкаЗаказовТовары.Документ = РаспределениеЗСЯ.Заказ
	|ГДЕ
	|	СборкаЗаказовТовары.Ссылка = &Ссылка
	|	И СборкаЗаказовТовары.КоличествоСобрано > 0";
	
	Запрос.УстановитьПараметр("Ссылка",СборкаЗаказаСсылка);
	Запрос.УстановитьПараметр("РаспределениеЗСЯ",РаспределениеЗСЯ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() цикл
		СтрокаКомплектации = ДокументКомплектация.Комплектация.Добавить();		
		ЗаполнитьЗначенияСвойств(СтрокаКомплектации,Выборка);
	КонецЦикла;
	
	ДокументКомплектация.Записать(РежимЗаписиДокумента.Проведение);
	
	СсылкаДокументКомплектация = ДокументКомплектация.Ссылка;
	
	Возврат Истина;
КонецФункции

Функция РаспределитьЗаказыСборкиПоЗСЯ(СборкаЗаказаСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СборкаЗаказовТовары.Документ,
	|	СборкаЗаказовТовары.Ссылка.Холод
	|ПОМЕСТИТЬ ВТЗаказы
	|ИЗ
	|	Документ.Сборка.Товары КАК СборкаЗаказовТовары
	|ГДЕ
	|	СборкаЗаказовТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СборкаЗаказовТовары.Документ,
	|	СборкаЗаказовТовары.Ссылка.Холод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ,
	|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ
	|ПОМЕСТИТЬ втЗаказыВЗСЯ
	|ИЗ
	|	РегистрСведений.КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯ.СрезПоследних(, ) КАК КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаказы КАК ВТЗаказы
	|		ПО КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ = ВТЗаказы.Документ
	|ГДЕ
	|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ.ВидМестаЗСЯ = ЗНАЧЕНИЕ(Перечисление.ВидыМестЗСЯ.СборкаСамовывозов)
	|	И КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ.ЗонаСсылка.Холод = &Холод
	|
	|СГРУППИРОВАТЬ ПО
	|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ,
	|	КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЗаказыВЗСЯ.ЗСЯ,
	|	втЗаказыВЗСЯ.Заказ
	|ИЗ
	|	втЗаказыВЗСЯ КАК втЗаказыВЗСЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МестоЗСЯ.Ссылка КАК ЗСЯ,
	|	МестоЗСЯ.Удобство КАК Удобство
	|ИЗ
	|	Справочник.МестоЗСЯ КАК МестоЗСЯ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯ.СрезПоследних(, ) КАК КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних
	|		ПО (КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.ЗСЯ = МестоЗСЯ.Ссылка)
	|ГДЕ
	|	(КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ = ЗНАЧЕНИЕ(Документ.Заказ.ПустаяСсылка)
	|			ИЛИ КомплектацияЗаказов_РаспределениеЗаказовПоЗСЯСрезПоследних.Заказ ЕСТЬ NULL)
	|	И МестоЗСЯ.ВидМестаЗСЯ = ЗНАЧЕНИЕ(Перечисление.ВидыМестЗСЯ.СборкаСамовывозов)
	|	И МестоЗСЯ.ЗонаСсылка.Холод = &Холод
	|	И МестоЗСЯ.ЗонаСсылка.Склад = &Склад
	|	И НЕ МестоЗСЯ.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	МестоЗСЯ.Ссылка,
	|	МестоЗСЯ.Удобство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Удобство УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТЗаказы.Документ), 0) КАК КоличествоЗаказов
	|ИЗ
	|	ВТЗаказы КАК ВТЗаказы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаказы.Документ КАК Заказ
	|ИЗ
	|	ВТЗаказы КАК ВТЗаказы";
	
	Запрос.УстановитьПараметр("Ссылка",СборкаЗаказаСсылка);
	Запрос.УстановитьПараметр("Холод",СборкаЗаказаСсылка.Холод);
	Запрос.УстановитьПараметр("Склад",СборкаЗаказаСсылка.Склад);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаРазложенныхЗаказов = МассивРезультатов[2].Выгрузить();
	
	ТаблицаСвободныхЗСЯ = МассивРезультатов[3].Выгрузить();
		
	ВыборкаЗаказов = МассивРезультатов[5].Выбрать();
	
	КоличестовЗаказов = 0;
	
	ВыборкаКоличестовЗаказов = МассивРезультатов[4].Выбрать();
	
	Пока ВыборкаКоличестовЗаказов.Следующий() цикл
		КоличестовЗаказов = ВыборкаКоличестовЗаказов.КоличествоЗаказов;
	КонецЦикла;
	
	КоличествоНеХватило = КоличестовЗаказов - (ТаблицаРазложенныхЗаказов.Количество() + ТаблицаСвободныхЗСЯ.Количество());
	
	Если КоличествоНеХватило > 0 тогда
		Возврат "Для комплектации заказа не хватает " + КоличествоНеХватило + " ячеек. Сообщите об этом системному администратору!!!";
	КонецЕсли;
	
	ТаблицаЗСЯ = Новый ТаблицаЗначений;
	ТаблицаЗСЯ.Колонки.Добавить("ЗСЯ",Новый ОписаниеТипов("СправочникСсылка.МестоЗСЯ"));
	ТаблицаЗСЯ.Колонки.Добавить("Заказ",Новый ОписаниеТипов("ДокументСсылка.Заказ"));
	
	Пока ВыборкаЗаказов.Следующий() цикл
		Строка = ТаблицаЗСЯ.Добавить();
		Строка.Заказ = ВыборкаЗаказов.Заказ;
		СтрокиРазложенных = ТаблицаРазложенныхЗаказов.НайтиСтроки(Новый Структура("Заказ",ВыборкаЗаказов.Заказ));
		Для каждого СтрокаРазложенных из СтрокиРазложенных цикл
			Строка.ЗСЯ = СтрокаРазложенных.ЗСЯ;
			Прервать;
		КонецЦикла;
		Если не ЗначениеЗаполнено(Строка.ЗСЯ) тогда
			Строка.ЗСЯ = ТаблицаСвободныхЗСЯ[0].ЗСЯ;
			ТаблицаСвободныхЗСЯ.Удалить(0);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаЗСЯ;
	
КонецФункции
